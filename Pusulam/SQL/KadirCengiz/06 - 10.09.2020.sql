
PRINT N'Altering [dbo].[sp_FiltreEk]...';
ALTER procedure [dbo].[sp_FiltreEk]
	@ISLEM			INT				= NULL,
	@TCKIMLIKNO		VARCHAR(11)		= NULL,
	@OTURUM			VARCHAR(36)		= NULL,
	@ID_MENU		INT				= NULL,
	
	@DONEM			INT				= NULL,

	@ID_SUBELIST	VARCHAR(MAX)	= NULL,
	@ID_KADEME3LIST	VARCHAR(MAX)	= NULL,
	
	@ID_SUBE		INT				= NULL,
	@ID_KADEME3		INT				= NULL,
	@ID_KADEME		INT				= NULL,
	@ID_SINIF		INT				= NULL,
	@TC_OGRENCI		VARCHAR(11)		= NULL,
	@ID_DERSLIST	VARCHAR(MAX)	= NULL,
	@ID_SINAVTURU	INT				= NULL,
	@SINAVDURUM		BIT				= NULL,
	@ONLINESINAV	BIT				= NULL
AS
BEGIN	
	
	DECLARE @PROCNAME VARCHAR(MAX) = (SELECT OBJECT_NAME(@@PROCID))
	DECLARE @LOGJSON VARCHAR(MAX)
	SET @LOGJSON = (
		SELECT	 ISLEM			= @ISLEM
				,TCKIMLIKNO		= @TCKIMLIKNO
				,OTURUM			= @OTURUM
				,ID_MENU		= @ID_MENU

				,DONEM			= @DONEM
								
				,ID_SUBE		= @ID_SUBE
				,ID_KADEME3		= @ID_KADEME3
				,ID_KADEME		= @ID_KADEME
				,ID_SINIF		= @ID_SINIF
								
				,ID_SUBELIST	= @ID_SUBELIST
				,ID_KADEME3LIST	= @ID_KADEME3LIST
				,TC_OGRENCI		= @TC_OGRENCI
				,ID_DERSLIST	= @ID_DERSLIST
				,ID_SINAVTURU	= @ID_SINAVTURU
				,SINAVDURUM		= @SINAVDURUM
				,ONLINESINAV	= @ONLINESINAV
		FOR JSON PATH, WITHOUT_ARRAY_WRAPPER	   
	)	

	DECLARE @ID_LOG INT = 0

	BEGIN TRY    
		EXEC @ID_LOG = dbo.sp_OturumKontrolMenuYetkiLog @OTURUM = @OTURUM, @TCKIMLIKNO = @TCKIMLIKNO, @ID_MENU = @ID_MENU, @LOGJSON = @LOGJSON, @ISLEM = @ISLEM, @PROSEDURADI = @PROCNAME
		
		IF @ID_LOG > 0
		BEGIN

			BEGIN
				DROP TABLE IF EXISTS #SUBELIST
				DROP TABLE IF EXISTS #KADEMELIST
				DROP TABLE IF EXISTS #KADEME3LIST
				DROP TABLE IF EXISTS #SINIFLIST
				DROP TABLE IF EXISTS #DERSLIST

				CREATE TABLE #SUBELIST (ID_SUBE INT)
				IF @ID_SUBELIST IS NOT NULL
					INSERT INTO #SUBELIST (ID_SUBE) SELECT VALUE FROM OPENJSON(@ID_SUBELIST)  
				INSERT INTO #SUBELIST (ID_SUBE) SELECT @ID_SUBE WHERE @ID_SUBE IS NOT NULL

				CREATE TABLE #KADEME3LIST (ID_KADEME3 INT)
				IF @ID_KADEME3LIST IS NOT NULL	
					INSERT INTO #KADEME3LIST  (ID_KADEME3) SELECT VALUE FROM OPENJSON(@ID_KADEME3LIST) 
				INSERT INTO #KADEME3LIST  (ID_KADEME3) SELECT @ID_KADEME3 WHERE @ID_KADEME3 IS NOT NULL
										
				DECLARE @AKTIFDONEM INT = (SELECT TOP 1 DONEM FROM OkyanusDB.DBO.v3AktifDonem WHERE AKTIF = 1 ORDER BY DONEM DESC)

				DROP TABLE IF EXISTS #MENU_KT

				SELECT DISTINCT KY.ID_KULLANICITIPI, KY.ID_KADEME 
				INTO #MENU_KT
				FROM MenuYetki							MK
				JOIN OKYANUSDB.DBO.VW_KullaniciYetki	KY	ON	KY.ID_KULLANICITIPI = MK.ID_KULLANICITIPI
															AND	KY.ID_KADEME		= MK.ID_KADEME
				WHERE	ID_MENU			= @ID_MENU
					AND	KY.TCKIMLIKNO	= @TCKIMLIKNO
				UNION
				-- KİŞİ YETKİ VARSA ADMİN GİBİ 
				SELECT DISTINCT 1, K.ID_KADEME
				FROM MenuKullaniciYetki					MK
				CROSS APPLY OkyanusDB.dbo.v3Kademe		K
				WHERE	ID_MENU			= @ID_MENU
					AND	MK.TCKIMLIKNO	= @TCKIMLIKNO
				-- GENEL HAK VAR İSE MENU YETKİYE GEREK YOK
				UNION
				SELECT DISTINCT 1, K.ID_KADEME
				FROM OkyanusDB.dbo.v3Kademe K
				WHERE dbo.fn_GenelHak (@TCKIMLIKNO) = 1

				IF @DONEM IS NULL
				BEGIN
					SET @DONEM = @AKTIFDONEM
				END
			END
			
			IF @ISLEM = 1	-- DERS LİSTE			
			BEGIN
				
				DROP TABLE IF EXISTS #KADEME3_KT

				SELECT ID_KULLANICITIPI INTO #KADEME3_KT FROM v3KullaniciTipi
				WHERE ID_KULLANICITIPI IN ( 1, 26, 27, 53, 54, 59, 60, 61, 91, 93, 94)

				IF EXISTS ( SELECT TOP 1 M.ID_KULLANICITIPI FROM #MENU_KT	M JOIN #KADEME3_KT	SB	ON	SB.ID_KULLANICITIPI	= M.ID_KULLANICITIPI ) 
				BEGIN
					SELECT ISNULL((
						SELECT DISTINCT D.ID_DERS, D.DERSAD + ' (' + K3.AD + ')' AS AD
						FROM eokul_v2.dbo.Ders D
						INNER JOIN OkyanusDB.dbo.v3Kademe3 K3 ON K3.ID_KADEME3 = D.ID_KADEME3
						WHERE AKTIF = 1 AND IPTAL = 0 
						AND ((EXISTS ( SELECT * FROM #KADEME3LIST )	AND  D.ID_KADEME3	IN ( SELECT ID_KADEME3	FROM #KADEME3LIST ))	OR	(NOT EXISTS ( SELECT * FROM #KADEME3LIST )	AND @ID_KADEME3LIST IS NULL ))
						ORDER BY AD
						FOR JSON PATH
					),'[]')
				END
				ELSE
				BEGIN
					SELECT ISNULL((
						SELECT DISTINCT D.ID_DERS, D.DERSAD + ' (' + K3.AD + ')' AS AD
						FROM eokul_v2.dbo.Ders						D
						INNER JOIN OkyanusDB.dbo.v3Kademe3			K3 ON K3.ID_KADEME3 = D.ID_KADEME3
						INNER JOIN eokul_v2.dbo.DersOgretmen		DO	ON DO.ID_DERS = D.ID_DERS
						INNER JOIN OkyanusDB.dbo.vw_v4SinifDetay	S	ON S.ID_SINIF = DO.ID_SINIF
						WHERE	AKTIF = 1 AND IPTAL = 0 AND DO.TC_OGRETMEN = @TCKIMLIKNO AND DO.DONEMKOD = @DONEM
							AND ((EXISTS ( SELECT * FROM #SUBELIST )	AND  S.ID_SUBE		IN ( SELECT ID_SUBE		FROM #SUBELIST ))		OR	(NOT EXISTS ( SELECT * FROM #SUBELIST )		AND @ID_SUBELIST IS NULL ))
							AND ((EXISTS ( SELECT * FROM #KADEME3LIST )	AND  D.ID_KADEME3	IN ( SELECT ID_KADEME3	FROM #KADEME3LIST ))	OR	(NOT EXISTS ( SELECT * FROM #KADEME3LIST )	AND @ID_KADEME3LIST IS NULL ))
						ORDER BY AD
						FOR JSON PATH
					),'[]')
				END
				
				DROP TABLE IF EXISTS #KADEME3_KT
			END

			IF @ISLEM = 2	-- KADEME ÖDEV TÜR LİSTELE - SELECT
			BEGIN
				SELECT
				ISNULL(
				(
					SELECT OT.ID_ODEVTUR, OT.AD
					FROM OdevTur OT
					INNER JOIN OdevTurKademe OTK ON OTK.ID_ODEVTUR = OT.ID_ODEVTUR AND OTK.ID_KADEME = @ID_KADEME
					ORDER BY OT.ID_ODEVTUR
					FOR JSON PATH
				), '[]')
			END

			IF @ISLEM = 3	-- ÖĞRENCİ ODEV DERS LİSTELE
			BEGIN
				SELECT D.ID_DERS, D.DERSAD
				INTO #OGRENCIODEV3
				FROM OdevSinifOgrenci OSO 
				INNER JOIN OdevSinif OS ON OS.ID_ODEVSINIF = OSO.ID_ODEVSINIF
				INNER JOIN Odev O ON O.ID_ODEV = OS.ID_ODEV
				LEFT JOIN eokul_v2.dbo.Ders D ON D.ID_DERS = O.ID_DERS
				WHERE OSO.TCKIMLIKNO = @TC_OGRENCI
				AND O.AKTIF = 1
				AND O.DONEM = @DONEM

				SELECT
				ISNULL(
				(
					SELECT * FROM 
					(
						SELECT 0 AS TUR, 0 AS ID_DERS, 'Genel ('+ CAST(COUNT(1) AS VARCHAR) + ')' AS AD
						FROM #OGRENCIODEV3
						UNION
						SELECT 1 AS TUR, ID_DERS, DERSAD + ' (' + CAST(COUNT(1) AS VARCHAR) + ')' AS AD
						FROM #OGRENCIODEV3
						WHERE ID_DERS IS NOT NULL
						GROUP BY ID_DERS, DERSAD
					) X
					ORDER BY TUR, AD
					FOR JSON PATH
				), '[]')

				DROP TABLE #OGRENCIODEV3
			END

			IF @ISLEM = 4	-- ÖĞRETMEN LİSTELE
			BEGIN
				SELECT
				ISNULL(
				(
					SELECT DISTINCT K.TCKIMLIKNO, K.AD + ' ' + K.SOYAD AS ADSOYAD
					FROM OkyanusDB.dbo.v3Kullanici K
					INNER JOIN OkyanusDB.dbo.v3SubeYetki SY ON SY.TCKIMLIKNO = K.TCKIMLIKNO
					INNER JOIN eokul_v2.dbo.DersOgretmen DO ON DO.TC_OGRETMEN = SY.TCKIMLIKNO
					LEFT JOIN eokul_v2.dbo.Ders D ON D.ID_DERS = DO.ID_DERS AND ((EXISTS ( SELECT * FROM #KADEME3LIST )	AND  D.ID_KADEME3	IN ( SELECT ID_KADEME3	FROM #KADEME3LIST ))	OR	(NOT EXISTS ( SELECT * FROM #KADEME3LIST )	AND @ID_KADEME3LIST IS NULL ))
																			AND D.AKTIF = 1 AND D.IPTAL = 0
					WHERE
					((EXISTS ( SELECT * FROM #SUBELIST )	AND  SY.ID_SUBE		IN ( SELECT ID_SUBE		FROM #SUBELIST ))		OR	(NOT EXISTS ( SELECT * FROM #SUBELIST )		AND @ID_SUBELIST IS NULL ))					
					AND DO.DONEMKOD = @DONEM
					AND (@ID_DERSLIST = '[]' OR @ID_DERSLIST IS NULL OR DO.ID_DERS IN (SELECT VALUE FROM OPENJSON(@ID_DERSLIST)))
					ORDER BY ADSOYAD
					FOR JSON PATH
				), '[]')
			END

			IF @ISLEM = 5	-- BRANŞ LİSTELE
			BEGIN
				SELECT 
				ISNULL(
				(
					SELECT DISTINCT BRANS FROM OkyanusDB.dbo.v3Ogretmen WHERE BRANS IS NOT NULL ORDER BY BRANS 
					FOR JSON PATH
				), '[]')
			END

			IF @ISLEM = 6	-- SINAV TÜRÜ LİSTELE
			BEGIN
				SELECT
				ISNULL(
					(
						SELECT ST.ID_SINAVTURU, ST.AD
						FROM Sinav S
						JOIN SinavTuru ST ON ST.ID_SINAVTURU = S.ID_SINAVTURU
						WHERE (S.ID_KADEME3 = @ID_KADEME3 OR @ID_MENU = 1332) AND S.DONEM = @DONEM AND S.AKTIF = 1 AND (S.DURUM = 2 OR @SINAVDURUM = 1)
						GROUP BY ST.ID_SINAVTURU, ST.AD
						ORDER BY ST.AD
						FOR JSON PATH
					),
					'[]'
				)
			END

			IF @ISLEM = 7	-- SINAV LİSTELE
			BEGIN
				SELECT
				ISNULL(
					(
						SELECT S.ID_SINAV, '(' + CONVERT(varchar(10), S.SINAVTARIH, 104) + ') ' + S.AD AS AD
						FROM Sinav S
						WHERE	( S.ID_KADEME3	= @ID_KADEME3 OR @ID_MENU = 1332 ) -- OnlineSinavOgrenciAtama
							AND S.ID_SINAVTURU	= @ID_SINAVTURU
							AND S.DONEM			= @DONEM 
							AND S.AKTIF			= 1 
							--AND S.DURUM			= 2 
							AND (	(@ONLINESINAV = 1 AND S.ONLINESINAV = 1	)
								OR	(@ONLINESINAV IS NULL AND S.DURUM	= 2 )
							)
						ORDER BY S.ID_KADEME3, S.SINAVTARIH DESC
						FOR JSON PATH
					),
					'[]'
				)
			END
			
			DROP TABLE IF EXISTS #SUBELIST
			DROP TABLE IF EXISTS #KADEME3LIST
			DROP TABLE IF EXISTS #MENU_KT
		END 
	END TRY
	BEGIN CATCH
		DECLARE @ErrorSeverity INT;
		DECLARE @ErrorState INT;

		SELECT 
			@ErrorSeverity 	= ERROR_SEVERITY(),
			@ErrorState		= ERROR_STATE()
				
		DECLARE @MSG VARCHAR(4000)
		SELECT @MSG = ERROR_MESSAGE()

		EXEC [dbo].[sp_CustomRaiseError] @MESSAGE = @MSG, @SEVERITY = @ErrorSeverity , @STATE = @ErrorState , @ID_LOG = @ID_LOG, @ID_LOGTUR = 2
	END CATCH;
END
GO
PRINT N'Altering [dbo].[sp_OnlineDers]...';


GO
ALTER procedure [dbo].[sp_OnlineDers]
	@ISLEM						INT				= NULL,
	@TCKIMLIKNO					VARCHAR(11)		= NULL,
	@OTURUM						VARCHAR(36)		= NULL,
	@ID_MENU					INT				= NULL,
	
	@TARIH						NVARCHAR(MAX)	= NULL,
	@TC_OGRENCI					NVARCHAR(11)	= NULL,
	@ID_SINIF					INT				= NULL,
	@ID_KADEME3					INT				= NULL,
	@ID_ONLINEDERSSINIFJITSI	INT				= NULL,
	@EKRANPAYLASIMID			VARCHAR(MAX)	= NULL,
	@TC_OGRETMEN				VARCHAR(MAX)	= NULL,
	@LINK						VARCHAR(MAX)	= NULL,
	@HERKESISUSTUR				VARCHAR(MAX)	= NULL,
	@SQLJSON					NVARCHAR(MAX)	= NULL

AS
BEGIN
/*

	Değiştiren Adı		: Kadir Cengiz
	Değiştirilen Tarih	: 10.09.20202
	
	--	sp Alter 
		--	ISLEM = 16,17 Add
	--	ÖĞRETMEN ONLINE DERS PROGRAMI, KALDIR

*/
		
	DECLARE @PROCNAME VARCHAR(MAX) = (SELECT OBJECT_NAME(@@PROCID))
	DECLARE @LOGJSON VARCHAR(MAX)
	SET @LOGJSON = (
		SELECT	 ISLEM						= @ISLEM
				,TCKIMLIKNO					= @TCKIMLIKNO
				,OTURUM						= @OTURUM
				,ID_MENU					= @ID_MENU

				,SQLJSON					= @SQLJSON
				,TARIH						= @TARIH
				,ID_SINIF					= @ID_SINIF
				,ID_KADEME3					= @ID_KADEME3
				,TC_OGRETMEN				= @TC_OGRETMEN
				,TC_OGRENCI					= @TC_OGRENCI
				,LINK						= @LINK
				,HERKESISUSTUR				= @HERKESISUSTUR
				,ID_ONLINEDERSSINIFJITSI	= @ID_ONLINEDERSSINIFJITSI
				,EKRANPAYLASIMID			= @EKRANPAYLASIMID
		FOR JSON PATH, WITHOUT_ARRAY_WRAPPER	   
	)	

	--IF @TCKIMLIKNO != '45964597234'
	--BEGIN
	--	RAISERROR ('Lütfen Daha Sonra Tekrar Deneyiniz' , 16,1);
	--	return
	--END
	DECLARE @_ID_LOG INT = 0

	BEGIN TRY    
		DECLARE @_DESTEK_KONTROL INT
		EXEC @_ID_LOG = dbo.sp_OturumKontrolMenuYetkiLog @OTURUM = @OTURUM, @TCKIMLIKNO = @TCKIMLIKNO, @ID_MENU = @ID_MENU, @LOGJSON = @LOGJSON, @ISLEM = @ISLEM, @PROSEDURADI = @PROCNAME
		
		IF @_ID_LOG > 0
		BEGIN
				
			DECLARE @AKTIFDONEM VARCHAR(4) = (SELECT OkyanusDB.dbo.fn_AktifDonem())
			
			IF @ISLEM = 1	--	GENEL KLASÖR LİSTESİ					
			BEGIN
			
				SELECT ISNULL((
					SELECT DISTINCT 
						 K3.ID_KADEME3
						,K3.AD
						,K2.ID_KADEME2
						,K2.AD
						,LINK	= ISNULL((
							SELECT GK.LINK
							FROM OnlineDersGenelKlasor	GK	
							WHERE	GK.ID_KADEME2	= KB.ID_KADEME2
								AND	GK.ID_KADEME3	= KB.ID_KADEME3
						),'')
					FROM v3Kademe3					K3
					JOIN v3KademeBilgi				KB	ON	KB.ID_KADEME3	= K3.ID_KADEME3
					JOIN v3Kademe2					K2	ON	K2.ID_KADEME2	= KB.ID_KADEME2
					FOR JSON AUTO
				),'[]')

			END

			IF @ISLEM = 2	--	GENEL KLASÖR KAYDET						
			BEGIN

				DELETE FROM OnlineDersGenelKlasor 

				INSERT INTO OnlineDersGenelKlasor (ID_KADEME2, ID_KADEME3, LINK, KYE_TCKIMLIKNO)
				SELECT ID_KADEME2, ID_KADEME3, LINK, @TCKIMLIKNO
				FROM OPENJSON(@SQLJSON)
				WITH(
					ID_KADEME3	INT,
					K2			NVARCHAR(MAX) AS JSON
				) AS KADEME3
				CROSS APPLY OPENJSON(KADEME3.K2) 
				WITH (
					ID_KADEME2	INT,
					LINK		VARCHAR(MAX)
				) KADEME2

			END

			IF @ISLEM = 3	--	TARIH LIST								
			BEGIN
			
				DECLARE @TARIHLIST TABLE (BASTARIH VARCHAR(MAX), BITTARIH VARCHAR(MAX))
				DECLARE @PAZARTESI DATE = DATEADD(week, DATEDIFF(day, 0, getdate())/7, 0)
				
				WHILE (SELECT DATEDIFF(DAY,@PAZARTESI,CAST('15.06.' + CAST(CAST((SELECT OkyanusDB.dbo.fn_AktifDonem()) AS INT) + 1 AS VARCHAR) AS date) )) > 0
				BEGIN				
	
					INSERT INTO @TARIHLIST (BASTARIH, BITTARIH)
					SELECT	BASTARIH = CONVERT(VARCHAR,@PAZARTESI,104),
							BITTARIH = CONVERT(VARCHAR,DATEADD(day,6, @PAZARTESI),104) 
			
					SET @PAZARTESI = DATEADD(WEEK,1,@PAZARTESI)
	
				END

				SELECT ISNULL((
					SELECT BASTARIH, BITTARIH
					FROM @TARIHLIST
					FOR JSON PATH
				),'[]')

			END

			IF @ISLEM = 4	--	SINIF TARIH DERS LINK GÖRÜNTÜLE			
			BEGIN
				SELECT ISNULL((
					SELECT G.ID_GUN
						,G.GUN
						,SA.DERSNO
						,SA.BASSAAT
						,SA.BITSAAT
						,SN = ISNULL((
							SELECT 
								 GSN.DERSAD
								,GSN.LINK
								,GSN.SIFRE
							FROM OnlineDersSinif	GSN	
							WHERE	GSN.DERSNO			= SA.DERSNO
								AND GSN.ID_GUN			= G.ID_GUN
								AND GSN.KYE_TCKIMLIKNO	= @TCKIMLIKNO
								AND GSN.ID_SINIF		= @ID_SINIF
								AND GSN.AKTIF			= 1
							FOR JSON PATH
						),'[]')
						,GENELDRIVEID = ISNULL((
							SELECT TOP 1 right(GK.LINK, charindex('/', reverse(GK.LINK) + '/') - 1)
							FROM OnlineDersGenelKlasor	GK
							JOIN v3SatisTuru			ST	ON	ST.ID_KADEME2	= GK.ID_KADEME2
															AND ST.ID_KADEME3	= GK.ID_KADEME3
							JOIN v3Sinif				SN	ON	SN.ID_SATISTURU	= ST.ID_SATISTURU
							WHERE SN.ID_SINIF = @ID_SINIF
						), '')
					FROM Gun					G
					CROSS APPLY OnlineDersSaat	SA
					ORDER BY G.ID_GUN, SA.DERSNO
					FOR JSON AUTO, INCLUDE_NULL_VALUES
				),'[]')

			END

			IF @ISLEM = 5	--	SINIF TARIH DERS LINK KAYDET			
			BEGIN
							
				UPDATE OnlineDersSinif SET
					AKTIF = 0
				WHERE	ID_SINIF		= @ID_SINIF
					AND	KYE_TCKIMLIKNO	= @TCKIMLIKNO
					AND	AKTIF			= 1

				INSERT INTO OnlineDersSinif (ID_SINIF, ID_GUN, DERSNO, DERSAD, LINK, SIFRE, KYE_TCKIMLIKNO)
				SELECT @ID_SINIF, ID_GUN, DERSNO, DERSAD, LINK, SIFRE, @TCKIMLIKNO
				FROM OPENJSON(@SQLJSON)
				WITH (
					ID_GUN	INT,
					SA		NVARCHAR(MAX) AS JSON
				) AS T
				CROSS APPLY OPENJSON(T.SA) 
				WITH (
					DERSNO	INT,
					SN		NVARCHAR(MAX) AS JSON
				) AS S
				CROSS APPLY OPENJSON(S.SN) 
				WITH (
					DERSAD	VARCHAR(MAX),
					LINK	VARCHAR(MAX),
					SIFRE	VARCHAR(MAX)
				) AS N
				WHERE	DERSAD	IS NOT NULL
					AND LINK	IS NOT NULL
					AND LINK	!= ''
					AND DERSAD	!= ''

			END

			IF @ISLEM = 6	--	ÖĞRENCİ ONLINE DERS PROGRAM				
			BEGIN				
				SELECT(
					SELECT (
						SELECT ISNULL((
							SELECT G.ID_GUN
								,G.GUN
								,SA.DERSNO
								,SA.BASSAAT
								,SA.BITSAAT
								,JITSIID= ISNULL(SNJ.ID_ONLINEDERSSINIFJITSI,'')
								,JITSIBASLIK= ISNULL(SNJ.BASLIK,'')
								,JITSIODAAD	= ISNULL(SNJ.ODAAD,'')
								,JITSISIFRE	= ISNULL(SNJ.SIFRE,'')
								,JITSIEKRANPAYLASIMID = ISNULL(SNJ.EKRANPAYLASIMID, '')
								,OGRETMENKLASOR = ISNULL((SELECT TOP 1 LINK FROM OnlineDersOgretmenKlasor K WHERE K.KYE_TCKIMLIKNO = SNJ.KYE_TCKIMLIKNO AND K.ID_KADEME3 = (SELECT ID_KADEME3 FROM v3Sinif WHERE ID_SINIF = SNJ.ID_SINIF)), '')
								,SN.DERSAD
								,LINK		= ISNULL(SN.LINK,'')
								,SIFRE		= ISNULL(SN.SIFRE,'')
								,ADSOYAD	= (
									SELECT CONCAT_WS(' ', K.AD, K.SOYAD)
									FROM v3Kullanici	K
									WHERE	K.TCKIMLIKNO	= SN.KYE_TCKIMLIKNO
										AND K.AKTIF			= 1
								)
								,SINIFAD	= ISNULL((
									SELECT S.AD
									FROM OkyanusDB.dbo.vw_Sinif	S
									WHERE S.ID_SINIF	= SN.ID_SINIF
								),'')
							FROM Gun							G
							CROSS APPLY OnlineDersSaat			SA
							LEFT JOIN OnlineDersSinif			SN	ON	SN.DERSNO	= SA.DERSNO
																	AND SN.ID_GUN	= G.ID_GUN
																	AND SN.AKTIF	= 1
																	AND SN.ID_SINIF	= (SELECT TOP 1 ID_SINIF FROM v3Ogrenci WHERE TCKIMLIKNO = @TC_OGRENCI)
							LEFT JOIN OnlineDersSinifJitsi		SNJ	ON	SNJ.DERSNO	 = SA.DERSNO
																	AND SNJ.ID_SINIF = (SELECT TOP 1 ID_SINIF FROM v3Ogrenci WHERE TCKIMLIKNO = @TC_OGRENCI)
																	AND SNJ.ID_GUN	 = G.ID_GUN
																	AND SNJ.AKTIF	 = 1
							ORDER BY G.ID_GUN, SA.DERSNO															
							FOR JSON AUTO, INCLUDE_NULL_VALUES
						),'[]') 
					) AS LISTE,
					(	SELECT LINK
						FROM OnlineDersGenelKlasor	GK
						JOIN v3SatisTuru			ST	ON	ST.ID_KADEME2	= GK.ID_KADEME2
														AND ST.ID_KADEME3	= GK.ID_KADEME3
						JOIN v3Sinif				SN	ON	SN.ID_SATISTURU	= ST.ID_SATISTURU
						JOIN v3Ogrenci				O	ON	O.ID_SINIF		= SN.ID_SINIF
						WHERE O.TCKIMLIKNO = @TC_OGRENCI
					) AS GENELKLASOR,
					GENELKLASORID = (
						SELECT TOP 1 right(GK.LINK, charindex('/', reverse(GK.LINK) + '/') - 1)
						FROM OnlineDersGenelKlasor	GK
						JOIN v3SatisTuru			ST	ON	ST.ID_KADEME2	= GK.ID_KADEME2
														AND ST.ID_KADEME3	= GK.ID_KADEME3
						JOIN v3Sinif				SN	ON	SN.ID_SATISTURU	= ST.ID_SATISTURU
						JOIN v3Ogrenci				O	ON	O.ID_SINIF		= SN.ID_SINIF
						WHERE O.TCKIMLIKNO = @TC_OGRENCI
					),
					(	SELECT TOP 1 SUBEAD + '-' + SINIF
						FROM vw_SubeGrupSinif	SN
						JOIN v3Ogrenci			O	ON	O.ID_SINIF		= SN.ID_SINIF
						WHERE O.TCKIMLIKNO = @TC_OGRENCI				
					) AS DOSYAAD
					FOR JSON PATH
				)

			END

			IF @ISLEM = 7	--	SINIF ONLINE DERS PROGRAMI				
			BEGIN
				SELECT (		
				SELECT LISTE = ISNULL((
					SELECT G.ID_GUN
						,G.GUN
						,SA.DERSNO
						,SA.BASSAAT
						,SA.BITSAAT
						,JITSIID= ISNULL(SNJ.ID_ONLINEDERSSINIFJITSI,'')
						,JITSIBASLIK= ISNULL(SNJ.BASLIK,'')
						,JITSIODAAD	= ISNULL(SNJ.ODAAD,'')
						,JITSISIFRE	= ISNULL(SNJ.SIFRE,'')
						,JITSIEKRANPAYLASIMID = ISNULL(SNJ.EKRANPAYLASIMID, '')
						,OGRETMENKLASOR = ISNULL((SELECT TOP 1 LINK FROM OnlineDersOgretmenKlasor K WHERE K.KYE_TCKIMLIKNO = SNJ.KYE_TCKIMLIKNO AND K.ID_KADEME3 = (SELECT ID_KADEME3 FROM v3Sinif WHERE ID_SINIF = @ID_SINIF)), '')
						,SN.DERSAD
						,LINK		= ISNULL(SN.LINK,'')
						,SIFRE		= ISNULL(SN.SIFRE,'')
						,ADSOYAD	= (
							SELECT CONCAT_WS(' ', K.AD, K.SOYAD)
							FROM v3Kullanici	K
							WHERE	K.TCKIMLIKNO	= SN.KYE_TCKIMLIKNO
								AND K.AKTIF			= 1
						)
					FROM Gun							G
					CROSS APPLY OnlineDersSaat			SA
					LEFT JOIN OnlineDersSinif			SN	ON	SN.DERSNO	 = SA.DERSNO
															AND SN.ID_SINIF	 = @ID_SINIF
															AND SN.ID_GUN	 = G.ID_GUN
															AND SN.AKTIF	 = 1
					LEFT JOIN OnlineDersSinifJitsi		SNJ	ON	SNJ.DERSNO	 = SA.DERSNO
															AND SNJ.ID_SINIF = @ID_SINIF
															AND SNJ.ID_GUN	 = G.ID_GUN
															AND SNJ.AKTIF	 = 1
					ORDER BY G.ID_GUN, SA.DERSNO															
					FOR JSON AUTO, INCLUDE_NULL_VALUES
				),'[]'),
				GENELKLASOR = (
					SELECT TOP 1 LINK
					FROM OnlineDersGenelKlasor	GK
					JOIN v3SatisTuru			ST	ON	ST.ID_KADEME2	= GK.ID_KADEME2
													AND ST.ID_KADEME3	= GK.ID_KADEME3
					JOIN v3Sinif				SN	ON	SN.ID_SATISTURU	= ST.ID_SATISTURU
					WHERE SN.ID_SINIF = @ID_SINIF
				),
				GENELKLASORID = (
					SELECT TOP 1 right(GK.LINK, charindex('/', reverse(GK.LINK) + '/') - 1)
					FROM OnlineDersGenelKlasor	GK
					JOIN v3SatisTuru			ST	ON	ST.ID_KADEME2	= GK.ID_KADEME2
													AND ST.ID_KADEME3	= GK.ID_KADEME3
					JOIN v3Sinif				SN	ON	SN.ID_SATISTURU	= ST.ID_SATISTURU
					WHERE SN.ID_SINIF = @ID_SINIF
				),
				DOSYAAD = (
					SELECT TOP 1 SUBEAD + ' - ' + SINIF
					FROM vw_SubeGrupSinif
					WHERE ID_SINIF = @ID_SINIF
				)
				FOR JSON PATH, WITHOUT_ARRAY_WRAPPER)
			END

			IF @ISLEM = 8	--	DERS PROGRAMI RAPOR						
			BEGIN
				SELECT * FROM (
					SELECT	 G.GUN
							,SA.DERSNO
							,SA.BASSAAT
							,SA.BITSAAT
							,DERSAD		= ISNULL((
								SELECT STUFF((
									SELECT '<hr />' +
										CASE	WHEN SN.DERSAD IS NULL THEN '<center>-</center>' 
												ELSE '<div style="font-size:8;margin:8px;">' +
													ISNULL(SN.DERSAD, '') + '</br>' 
													+ ISNULL((
														SELECT CONCAT_WS(' ', K.AD, K.SOYAD)
														FROM v3Kullanici	K
														WHERE	K.TCKIMLIKNO	= SN.KYE_TCKIMLIKNO
															AND K.AKTIF			= 1
													), '') 
													+ '</br><a href="' + ISNULL(SN.LINK,'') + '" target="_blank">' 
													+ (CASE WHEN @ID_SINIF = 0 THEN 'Derse Gitmek İçin Tıklayınız' ELSE SN.LINK END) + '.</a>
													</br> Şifre : '+
													ISNULL(SN.SIFRE,'')
													+'
													</div>'
											END
									FROM OnlineDersSinif	SN	
									WHERE	SN.DERSNO	= SA.DERSNO
									AND SN.ID_GUN	= G.ID_GUN
									AND SN.AKTIF	= 1
									AND (
											(@ID_SINIF = 0 AND SN.ID_SINIF	= (SELECT TOP 1 ID_SINIF FROM v3Ogrenci WHERE TCKIMLIKNO = @TC_OGRENCI)) 
											OR 
											(@ID_SINIF > 0 AND SN.ID_SINIF = @ID_SINIF)
										)
								FOR XML PATH(''), TYPE).value('.', 'VARCHAR(MAX)'), 1, 6, ''))
							,'<center>-</center>')
							
					FROM Gun							G
					CROSS APPLY OnlineDersSaat			SA
					--LEFT JOIN OnlineDersSinif			SN	ON	SN.DERSNO	= SA.DERSNO
					--										AND SN.ID_GUN	= G.ID_GUN
					--										AND SN.AKTIF	= 1
					--										AND (
					--												(@ID_SINIF = 0 AND SN.ID_SINIF	= (SELECT TOP 1 ID_SINIF FROM v3Ogrenci WHERE TCKIMLIKNO = @TC_OGRENCI)) 
					--												OR 
					--												(@ID_SINIF > 0 AND SN.ID_SINIF = @ID_SINIF)
					--											)
				) TABLEX
				PIVOT (
					MAX(DERSAD)
					FOR GUN IN ([PAZARTESİ], [SALI], [ÇARŞAMBA], [PERŞEMBE], [CUMA], [CUMARTESİ], [PAZAR])
				) PIVOTTABLE
				WHERE [PAZARTESİ] IS NOT NULL OR [SALI] IS NOT NULL OR [ÇARŞAMBA] IS NOT NULL OR [PERŞEMBE] IS NOT NULL OR [CUMA] IS NOT NULL OR [CUMARTESİ] IS NOT NULL OR [PAZAR] IS NOT NULL
				ORDER BY DERSNO
			END

			IF @ISLEM = 9	--	SINIF ONLINE DERS EKLE					
			BEGIN			
				IF EXISTS(SELECT * FROM OnlineDersSinifJitsi WHERE ID_SINIF = JSON_VALUE(@SQLJSON, '$.ID_SINIF') AND AKTIF = 1)
				BEGIN
					DECLARE @ADSOYAD VARCHAR(MAX) = (SELECT AD + ' ' + SOYAD FROM OnlineDersSinifJitsi S JOIN OkyanusDB.dbo.v3Kullanici K ON K.TCKIMLIKNO = S.KYE_TCKIMLIKNO WHERE S.ID_SINIF = JSON_VALUE(@SQLJSON, '$.ID_SINIF') AND S.AKTIF = 1)
					DECLARE @HATAMESAJ VARCHAR(MAX) = 'Sınıf için şu anda ' + @ADSOYAD + ' ders vermektedir!'
					EXEC [dbo].[sp_CustomRaiseError] @MESSAGE = @HATAMESAJ, @SEVERITY = 16, @STATE = 1, @ID_LOG = @_ID_LOG, @ID_LOGTUR = 3
				END
				ELSE
				BEGIN
					--UPDATE OnlineDersSinifJitsi SET
					--	AKTIF = 0
					--WHERE	ID_SINIF = JSON_VALUE(@SQLJSON, '$.ID_SINIF')
					--	AND	AKTIF	 = 1
					DECLARE @IDTABLE TABLE (ID INT)
					INSERT INTO OnlineDersSinifJitsi (ID_SINIF, ID_GUN, DERSNO, DERSAD, BASLIK, ODAAD, SIFRE, KYE_TCKIMLIKNO)
					OUTPUT inserted.ID_ONLINEDERSSINIFJITSI INTO @IDTABLE
					SELECT	 JSON_VALUE(@SQLJSON, '$.ID_SINIF')
							,JSON_VALUE(@SQLJSON, '$.ID_GUN')
							,JSON_VALUE(@SQLJSON, '$.DERSNO')
							,JSON_VALUE(@SQLJSON, '$.DERSAD')
							,JSON_VALUE(@SQLJSON, '$.BASLIK')
							,JSON_VALUE(@SQLJSON, '$.ODAAD')
							,JSON_VALUE(@SQLJSON, '$.SIFRE')
							,@TCKIMLIKNO

					SELECT ID FROM @IDTABLE
				END
			END

			IF @ISLEM = 10	--	SINIF ONLINE DERS SİL					
			BEGIN							
				UPDATE OnlineDersSinifJitsi SET
					AKTIF = 0
				WHERE ID_ONLINEDERSSINIFJITSI = @ID_ONLINEDERSSINIFJITSI
			END

			IF @ISLEM = 11	--	SINIF ONLINE EKRAN PAYLAŞIM ID GÜNCELLE	
			BEGIN							
				UPDATE OnlineDersSinifJitsi SET
					EKRANPAYLASIMID = @EKRANPAYLASIMID
				WHERE ID_ONLINEDERSSINIFJITSI = @ID_ONLINEDERSSINIFJITSI
			END

			IF @ISLEM = 12	--	ÖĞRETMEN KADEME3 KLASOR GETİR			
			BEGIN
				SELECT ISNULL(
				(
					SELECT TOP 1 LINK
					FROM OnlineDersOgretmenKlasor
					WHERE KYE_TCKIMLIKNO = @TCKIMLIKNO AND ID_KADEME3 = @ID_KADEME3
				), '')
			END

			IF @ISLEM = 13	--	ÖĞRETMEN KADEME3 KLASOR KAYDET			
			BEGIN
				DELETE FROM OnlineDersOgretmenKlasor WHERE KYE_TCKIMLIKNO = @TCKIMLIKNO AND ID_KADEME3 = @ID_KADEME3
				INSERT INTO OnlineDersOgretmenKlasor (ID_KADEME3, LINK, KYE_TCKIMLIKNO)
				SELECT @ID_KADEME3, @LINK, @TCKIMLIKNO
			END

			IF @ISLEM = 14	--	SINIF ONLINE DERS HERKESI SUSTUR		
			BEGIN							
				UPDATE OnlineDersSinifJitsi SET
					HERKESISUSTUR = @HERKESISUSTUR
				WHERE ID_ONLINEDERSSINIFJITSI = @ID_ONLINEDERSSINIFJITSI
			END

			IF @ISLEM = 15	--	SINIF ONLINE DERS HERKESI SUSTUR GETİR	
			BEGIN							
				SELECT HERKESISUSTUR FROM OnlineDersSinifJitsi
				WHERE ID_ONLINEDERSSINIFJITSI = @ID_ONLINEDERSSINIFJITSI
			END

			

			IF @ISLEM = 16	--	ÖĞRETMEN ONLINE DERS PROGRAMI			
			BEGIN

				SELECT LISTE = ISNULL((
					SELECT	 G.ID_GUN
							,G.GUN
							,SA.DERSNO
							,SA.BASSAAT
							,SA.BITSAAT
							,SN.DERSAD
							,SINIFAD = (SELECT S.AD FROM V3sinif S WHERE S.ID_SINIF = SN.ID_SINIF)
					FROM Gun							G
					CROSS APPLY OnlineDersSaat			SA
					LEFT JOIN OnlineDersSinif			SN	ON	SN.DERSNO		= SA.DERSNO
															AND KYE_TCKIMLIKNO	= @TC_OGRETMEN
															AND SN.ID_GUN		= G.ID_GUN
															AND SN.AKTIF		= 1
					ORDER BY G.ID_GUN, SA.DERSNO
					FOR JSON AUTO, INCLUDE_NULL_VALUES
				),'[]')
				
				
			END
			IF @ISLEM = 17	--	ÖĞRETMEN ONLINE DERS PROGRAMI KALDIR	
			BEGIN

				UPDATE OnlineDersSinif SET
					AKTIF = 0
				WHERE KYE_TCKIMLIKNO = @TC_OGRETMEN
				
			END

		END
	END TRY
	BEGIN CATCH
		DECLARE @_ErrorSeverity INT;
		DECLARE @_ErrorState INT;

		SELECT 
			@_ErrorSeverity	= ERROR_SEVERITY(),
			@_ErrorState		= ERROR_STATE()
				
		DECLARE @_MSG VARCHAR(4000)
		SELECT @_MSG = ERROR_MESSAGE()

		EXEC [dbo].[sp_CustomRaiseError] @MESSAGE = @_MSG, @SEVERITY = @_ErrorSeverity, @STATE = @_ErrorState, @ID_LOG = @_ID_LOG, @ID_LOGTUR = 2
	END CATCH;
END