USE [Pusulam]
GO
/*

	Değiştiren Adı		: Kadir Cengiz
	Değiştirilen Tarih	: 25.08.20202
	
	--	sp Alter 
		--	ISLEM = 16 Add
	--	Öğrenci Atama Kaldırma 

*/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROC [dbo].[sp_OnlineSinav]
	@ISLEM					INT				= NULL,
	@TCKIMLIKNO				VARCHAR(11)		= NULL,
	@OTURUM					VARCHAR(36)		= NULL,
	@ID_MENU				INT				= NULL,

	@ID_SORU				INT				= NULL,
	@ID_SINAV				INT				= NULL,
	@ID_DERS				INT				= NULL,
	@ID_SINAVTURU			INT				= NULL,
	@ID_SINAVANAHTAR		INT				= NULL,
	@ID_CEVAP				INT				= NULL,
	@ID_ONLINESINAVOGRENCI	INT				= NULL,
	@SINAVOTURUM			INT				= NULL,
	@KYE_TARIH				DATETIME		= NULL,
	@TC_EKLEYEN				VARCHAR(MAX)	= NULL,
	@TC_OGRENCI				VARCHAR(MAX)	= NULL,
	@ID_SINIFLIST			VARCHAR(MAX)	= NULL,
	@TC_OGRENCILIST			VARCHAR(MAX)	= NULL,
	@JSON					VARCHAR(MAX)	= NULL,
	@SQLJSON				VARCHAR(MAX)	= NULL
AS
BEGIN

	DECLARE @PROCNAME VARCHAR(MAX) = (SELECT OBJECT_NAME(@@PROCID))
	DECLARE @LOGJSON VARCHAR(MAX)
	SET @LOGJSON = (
		SELECT	 ISLEM					= @ISLEM	
				,TCKIMLIKNO				= @TCKIMLIKNO
				,OTURUM					= @OTURUM
				,ID_MENU				= @ID_MENU
								
				,ID_SORU				= @ID_SORU
				,TC_EKLEYEN				= @TC_EKLEYEN
				,KYE_TARIH				= @KYE_TARIH
				,ID_SINAV				= @ID_SINAV
				,ID_DERS				= @ID_DERS
				,ID_SINAVTURU			= @ID_SINAVTURU
				,ID_ONLINESINAVOGRENCI	= @ID_ONLINESINAVOGRENCI
				,SINAVOTURUM			= @SINAVOTURUM
				,[JSON]					= @JSON
				,SQLJSON				= @SQLJSON
				,ID_SINIFLIST			= @ID_SINIFLIST
				,TC_OGRENCILIST			= @TC_OGRENCILIST
				,TC_OGRENCI				= @TC_OGRENCI
				,ID_SINAVANAHTAR		= @ID_SINAVANAHTAR
				,ID_CEVAP				= @ID_CEVAP
		FOR JSON PATH, WITHOUT_ARRAY_WRAPPER	   
	)	
	
	DECLARE @_ID_LOG INT = 0

	BEGIN TRY    
		DECLARE @_DESTEK_KONTROL INT
		EXEC @_ID_LOG = dbo.sp_OturumKontrolMenuYetkiLog @OTURUM = @OTURUM, @TCKIMLIKNO = @TCKIMLIKNO, @ID_MENU = @ID_MENU, @LOGJSON = @LOGJSON, @ISLEM = @ISLEM, @PROSEDURADI = @PROCNAME
		

		IF @_ID_LOG > 0
		BEGIN

			IF @ISLEM = 1	--	Sınav Listele
			BEGIN
				SELECT
				(
					SELECT *, CONVERT(decimal(4,2), (CONVERT(decimal(4,2), XTABLE.DOGRU)/CONVERT(decimal(4,2), (XTABLE.DOGRU + XTABLE.BOS + XTABLE.YANLIS))*100.0)) AS PERFORMANS  
					FROM
					(
						SELECT 
							SOA.ID_SINAV
							,ISNULL(OSO.ID_ONLINESINAVOGRENCI,0) AS ID_ONLINESINAVOGRENCI
							,(
								SELECT DISTINCT AD + ', ' AS [text()] 
								FROM v3Sinavders 
								WHERE ID_DERS IN 
									(
										SELECT SUBSD.ID_DERS 
										FROM		SoruBankasi.dbo.SoruDetay	SUBSD 
										INNER JOIN	SoruBankasi.dbo.SinavSoru	SUBSS	ON SUBSS.ID_SORU = SUBSD.ID_SORU
										WHERE SUBSS.ID_SINAV=SOA.ID_SINAV
									) 
								For XML PATH ('')
							) AS DERS
							,V.AD AS SINAVTURU
							,S.AD AS ADI
							,ISNULL(OSO.GIRIS_SAYISI, 0) AS GIRIS
							,ISNULL(CONVERT(VARCHAR, OSO.BAS_TARIH, 104), '-') AS SONGIRIS
							,cast(ISNULL(OSO.DURUM, 0) as int) AS DURUM
							,CAST(DATEPART(HOUR,OSO.BIT_TARIH-OSO.BAS_TARIH) AS VARCHAR)+':'+CAST(DATEPART(MINUTE,OSO.BIT_TARIH-OSO.BAS_TARIH) AS VARCHAR)+':'+CAST(DATEPART(SECOND,OSO.BIT_TARIH-OSO.BAS_TARIH) AS VARCHAR) AS SURE
							,COUNT(C.DEGER) AS DOGRU
							,(SELECT COUNT(*) - COUNT(C.DEGER) FROM OnlineSinavOgrenciCevap SUBOC WHERE SUBOC.ID_ONLINESINAVOGRENCI=OSO.ID_ONLINESINAVOGRENCI) AS YANLIS
							,(
								SELECT COUNT(*) - (COUNT(C.DEGER) + (SELECT COUNT(*) - COUNT(C.DEGER) FROM OnlineSinavOgrenciCevap SUBOC WHERE SUBOC.ID_ONLINESINAVOGRENCI=OSO.ID_ONLINESINAVOGRENCI))
								FROM SoruBankasi.dbo.Soru SUBS
								INNER JOIN SoruBankasi.dbo.SinavSoru SUBSS ON SUBSS.ID_SORU=SUBS.ID_SORU
								INNER JOIN SoruBankasi.dbo.Cevap	 SUBC  ON SUBC.ID_SORU=SUBS.ID_SORU
								WHERE SUBSS.ID_SINAV=SOA.ID_SINAV AND SUBSS.KITAPCIK='A' AND (
																(SUBS.ID_SORUTUR=2)						--	AÇIK UÇLU
															OR (SUBS.ID_SORUTUR=3)						--	DOĞRU YANLIŞ
															OR (SUBS.ID_SORUTUR=4 AND SUBC.DEGER='1')	--	TEST
															OR (SUBS.ID_SORUTUR=5 AND SUBC.DEGER<>'')	--	EŞLEŞTİRME
															OR (SUBS.ID_SORUTUR=6)						--	BOŞLUK DOLDURMA
															)
							) AS BOS,
							CASE WHEN (S.BAS_TARIH<=CAST(GETDATE() AS DATE) AND S.BAS_SAAT<=convert(varchar(10), GETDATE(), 108))
								  AND ((S.BIT_TARIH=CAST(GETDATE() AS DATE) AND S.BIT_SAAT>=convert(varchar(10), GETDATE(), 108))
										OR 
										(S.BIT_TARIH>CAST(GETDATE() AS DATE))
									)
							THEN 1 ELSE 0 END AS DEVAM
						FROM		SoruBankasi.dbo.SinavOgrenciAtama	SOA
						INNER JOIN	SoruBankasi.dbo.Sinav				S	ON S.ID_SINAV=SOA.ID_SINAV
						INNER JOIN	SoruBankasi.dbo.Varlik				V	ON V.ID_VARLIK=S.ID_SINAVTUR
						INNER JOIN	SoruBankasi.dbo.SinavSoru			SS	ON SS.ID_SINAV=S.ID_SINAV AND SS.KITAPCIK='A'
						INNER JOIN	SoruBankasi.dbo.Soru				SOR	ON SOR.ID_SORU=SS.ID_SORU 
						INNER JOIN	SoruBankasi.dbo.SoruDetay			SD	ON SD.ID_SORU=SS.ID_SORU 
						LEFT  JOIN	OnlineSinavOgrenci					OSO ON OSO.TCKIMLIKNO=SOA.TCKIMLIKNO AND OSO.ID_ONLINESINAV=SOA.ID_SINAV
						LEFT  JOIN	OnlineSinavOgrenciCevap				OC	ON OC.ID_ONLINESINAVOGRENCI=OSO.ID_ONLINESINAVOGRENCI AND OC.ID_SORU=SOR.ID_SORU
						LEFT  JOIN	SoruBankasi.dbo.Cevap				C	ON C.ID_SORU=SOR.ID_SORU AND (
																												(SOR.ID_SORUTUR=2 AND OC.CEVAPNO=C.CEVAPNO AND CAST(OC.CEVAP AS VARCHAR)=CAST(C.DEGER AS VARCHAR))		--	AÇIK UÇLU
																											OR (SOR.ID_SORUTUR=3 AND OC.CEVAPNO=C.CEVAPNO AND CAST(OC.CEVAP AS VARCHAR)=CAST(C.DEGER AS VARCHAR))		--	DOĞRU YANLIŞ
																											OR (SOR.ID_SORUTUR=4 AND C.DEGER='1' AND CAST(OC.CEVAP AS VARCHAR)=CAST(C.CEVAPNO AS VARCHAR))				--	TEST
																											OR (SOR.ID_SORUTUR=5 AND OC.CEVAPNO=C.CEVAPNO AND CAST(OC.CEVAP AS VARCHAR)=CAST(C.DEGER AS VARCHAR))		--	EŞLEŞTİRME
																											OR (SOR.ID_SORUTUR=6 AND OC.CEVAPNO=C.CEVAPNO AND CAST(OC.CEVAP AS VARCHAR)=CAST(C.DEGER AS VARCHAR))		--	BOŞLUK DOLDURMA
																											)
						WHERE (@ID_SINAV IS NULL OR SOA.ID_SINAV=@ID_SINAV) AND ID_SORUTUR<>1 AND ID_USTSORU IS NULL AND SOA.TCKIMLIKNO=@TCKIMLIKNO
							AND (@ID_SINAVTURU IS NULL OR @ID_SINAVTURU = 0 OR S.ID_SINAVTUR=@ID_SINAVTURU) AND (@ID_DERS IS NULL OR @ID_DERS = 0 OR SD.ID_DERS=@ID_DERS) --@TCKIMLIKNO AND 
						GROUP BY SOA.ID_SINAV, S.BAS_TARIH, S.BAS_SAAT, S.BIT_TARIH, S.BIT_SAAT, OSO.ID_ONLINESINAVOGRENCI, V.AD, S.AD, OSO.DURUM, OSO.GIRIS_SAYISI, OSO.BAS_TARIH, OSO.BIT_TARIH
					) XTABLE
					FOR JSON PATH, INCLUDE_NULL_VALUES
				) AS JSON
			END

			IF @ISLEM = 2	--	Soru Listesi Getir 
			BEGIN
				SELECT
				(
					SELECT 
						S.ID_SORU
						,S.ID_SORUTUR
						,S.ID_USTSORU
						,SORUNO= ROW_NUMBER() OVER (ORDER BY SS.SORUNO)
						,(SELECT
							(
								SELECT
									SORUNO= ROW_NUMBER() OVER (ORDER BY SS.SORUNO)
									,(
										 SELECT 
											 ID_SORU=ISNULL(ID_SORU,0)
											,ID_USTSORU=ISNULL(ID_USTSORU,0)
											,ID_SORUTUR=ISNULL(ID_SORUTUR,0)
											,SUBV.AD AS SORUTUR
											,KOD=ISNULL(KOD,0)
											,ID_SINAVGRUP=ISNULL(ID_SINAVGRUP,0)
											,ID_DERS=ISNULL(ID_DERS,0)
											,UNITE=ISNULL(UNITE,0)
											,ID_ZORLUKTUR =ISNULL(ID_ZORLUKTUR,0)
										 FROM SoruBankasi.dbo.SoruDetay SUBSD 
										 INNER JOIN SoruBankasi.dbo.Varlik SUBV ON SUBV.ID_VARLIK=SUBS.ID_SORUTUR
										 Where ID_SORU=SUBS.ID_SORU 
										 FOR JSON PATH
									 ) AS Soru,
									(SELECT * FROM SoruBankasi.dbo.SoruHtml  Where ID_SORU=SUBS.ID_SORU FOR JSON PATH)AS SoruHtmlListesi,
									(	
										SELECT C.*
										FROM SoruBankasi.dbo.Cevap C
										Where C.ID_SORU=SUBS.ID_SORU ORDER BY ID_CEVAP,C.CEVAPNO,LEN(DEGER) DESC FOR JSON PATH
									)AS CevapListesi,
									(SELECT * FROM SoruBankasi.dbo.SoruCozum Where ID_SORU=SUBS.ID_SORU FOR JSON PATH)AS CozumListesi
								FROM SoruBankasi.dbo.Soru SUBS 
								WHERE SUBS.ID_SORU = S.ID_SORU OR SUBS.ID_USTSORU = S.ID_SORU
								FOR JSON AUTO, INCLUDE_NULL_VALUES 
							) AS SoruPaketList
						)AS SoruPaketList
					FROM SoruBankasi.dbo.Soru S
					INNER JOIN SoruBankasi.dbo.SinavSoru SS ON SS.ID_SORU=S.ID_SORU
					WHERE ID_SINAV=@ID_SINAV AND SS.KITAPCIK='A' AND ID_SORUTUR<>1 AND ID_USTSORU IS NULL
					FOR JSON PATH, INCLUDE_NULL_VALUES
				) AS J
			END

			IF @ISLEM = 3	--	Soru Getir
			BEGIN
				SELECT
				(
					SELECT 
						 SORUNO = ROW_NUMBER() OVER(ORDER BY ID_SORU ASC)
						,(
							 SELECT 
								 ID_SORU=ISNULL(ID_SORU,0)
								,ID_USTSORU=ISNULL(ID_USTSORU,0)
								,ID_SORUTUR=ISNULL(ID_SORUTUR,0)
								,KOD=ISNULL(KOD,0)
								,ID_SINAVGRUP=ISNULL(ID_SINAVGRUP,0)
								,ID_DERS=ISNULL(ID_DERS,0)
								,UNITE=ISNULL(UNITE,0)
								,ID_ZORLUKTUR =ISNULL(ID_ZORLUKTUR,0)
							 FROM SoruBankasi.dbo.SoruDetay Where ID_SORU=S.ID_SORU 
							 FOR JSON PATH
						 ) AS Soru,
						(SELECT * FROM SoruBankasi.dbo.SoruHtml  Where ID_SORU=S.ID_SORU FOR JSON PATH)AS SoruHtmlListesi,
						(SELECT * FROM SoruBankasi.dbo.Cevap	 Where ID_SORU=S.ID_SORU ORDER BY  LEN(DEGER) DESC, CEVAPNO ASC FOR JSON PATH)AS CevapListesi,
						(SELECT * FROM SoruBankasi.dbo.SoruCozum Where ID_SORU=S.ID_SORU FOR JSON PATH)AS CozumListesi
					FROM SoruBankasi.dbo.Soru S 
					WHERE S.ID_SORU = @ID_SORU OR S.ID_USTSORU = @ID_SORU
					FOR JSON AUTO, INCLUDE_NULL_VALUES 
				) AS SoruPaketList
			END

			IF @ISLEM = 4	--	Sınav Türü Getir
			BEGIN
				SELECT
				(
					SELECT ID_VARLIK, AD FROM SoruBankasi.dbo.Varlik V
					WHERE ID_VARLIKTUR=5
					FOR JSON PATH
				) J
			END

			IF @ISLEM = 5	--	Giriş Ekle
			BEGIN
				IF NOT EXISTS(SELECT ID_ONLINESINAVOGRENCI FROM [OnlineSinavOgrenci] WHERE TCKIMLIKNO=@TCKIMLIKNO AND ID_ONLINESINAV=@ID_SINAV)
				BEGIN
					declare @ID table(ID int)
					INSERT INTO [dbo].[OnlineSinavOgrenci]
							   ([TCKIMLIKNO]
							   ,[ID_ONLINESINAV]
							   ,[DURUM]
							   ,[GIRIS_SAYISI]
							   ,[BAS_TARIH]
							   ,[BIT_TARIH])
						OUTPUT inserted.ID_ONLINESINAVOGRENCI into @ID 
						 VALUES
								(@TCKIMLIKNO
								,@ID_SINAV
								,0
								,1
								,GETDATE()
								,NULL)
					select ID from @ID
				END
				ELSE
				BEGIN
					UPDATE [dbo].[OnlineSinavOgrenci] SET [GIRIS_SAYISI]=[GIRIS_SAYISI]+1, [BAS_TARIH]=GETDATE(), [BIT_TARIH]=NULL WHERE TCKIMLIKNO=@TCKIMLIKNO AND ID_ONLINESINAV=@ID_SINAV
					SELECT ID_ONLINESINAVOGRENCI FROM [OnlineSinavOgrenci] WHERE TCKIMLIKNO=@TCKIMLIKNO AND ID_ONLINESINAV=@ID_SINAV
				END

			END

			IF @ISLEM = 6	--	Sınavı Bitir
			BEGIN
				DELETE FROM [dbo].[OnlineSinavOgrenciCevap] WHERE ID_ONLINESINAVOGRENCI=@ID_ONLINESINAVOGRENCI
			
				INSERT INTO [dbo].[OnlineSinavOgrenciCevap]
						   ([ID_SORU]
						   ,[ID_ONLINESINAVOGRENCI]
						   ,[CEVAPNO]
						   ,[CEVAP])
				SELECT ID_SORU, @ID_ONLINESINAVOGRENCI, CEVAPNO, CEVAP FROM OPENJSON(@JSON)
				WITH(
					 ID_SORU	INT				'$.ID_SORU' 
					,CEVAPNO	INT				'$.CEVAPNO' 
					,CEVAP		VARCHAR(MAX)	'$.CEVAP' 
				)

				UPDATE OnlineSinavOgrenci SET DURUM=1, BIT_TARIH=GETDATE() WHERE ID_ONLINESINAVOGRENCI=@ID_ONLINESINAVOGRENCI
			END

			IF @ISLEM = 7	--	Soru Listesi Getir Cevaplı
			BEGIN
				SELECT
				(
					SELECT 
						S.ID_SORU
						,S.ID_SORUTUR
						,S.ID_USTSORU
						,SORUNO= ROW_NUMBER() OVER (ORDER BY SS.SORUNO)
						,(SELECT
							(
								SELECT
									SORUNO= ROW_NUMBER() OVER (ORDER BY SS.SORUNO)
									,(
										 SELECT 
											 ID_SORU=ISNULL(ID_SORU,0)
											,ID_USTSORU=ISNULL(ID_USTSORU,0)
											,ID_SORUTUR=ISNULL(ID_SORUTUR,0)
											,SUBV.AD AS SORUTUR
											,KOD=ISNULL(KOD,0)
											,ID_SINAVGRUP=ISNULL(ID_SINAVGRUP,0)
											,ID_DERS=ISNULL(ID_DERS,0)
											,UNITE=ISNULL(UNITE,0)
											,ID_ZORLUKTUR =ISNULL(ID_ZORLUKTUR,0)
										 FROM SoruBankasi.dbo.SoruDetay SUBSD 
										 INNER JOIN SoruBankasi.dbo.Varlik SUBV ON SUBV.ID_VARLIK=SUBS.ID_SORUTUR
										 Where ID_SORU=SUBS.ID_SORU 
										 FOR JSON PATH
									 ) AS Soru,
									(SELECT * FROM SoruBankasi.dbo.SoruHtml  Where ID_SORU=SUBS.ID_SORU FOR JSON PATH)AS SoruHtmlListesi,
									(	
										SELECT C.*, OC.CEVAP, SECILI = CASE WHEN SUBS.ID_SORUTUR=4 THEN (CASE WHEN C.ID_CEVAP=(SELECT ID_CEVAP FROM SoruBankasi.dbo.Cevap WHERE ID_SORU=C.ID_SORU AND CEVAPNO=OC.CEVAP) THEN 1 ELSE 0 END) ELSE 0 END
										FROM SoruBankasi.dbo.Cevap C
										LEFT JOIN OnlineSinavOgrenciCevap OC ON OC.ID_SORU=C.ID_SORU AND (SUBS.ID_SORUTUR=4 OR (SUBS.ID_SORUTUR<>4 AND OC.CEVAPNO=C.CEVAPNO)) AND OC.ID_ONLINESINAVOGRENCI=@ID_ONLINESINAVOGRENCI
										Where C.ID_SORU=SUBS.ID_SORU ORDER BY ID_CEVAP,C.CEVAPNO,LEN(DEGER) DESC FOR JSON PATH
									)AS CevapListesi,
									(SELECT * FROM SoruBankasi.dbo.SoruCozum Where ID_SORU=SUBS.ID_SORU FOR JSON PATH)AS CozumListesi
								FROM SoruBankasi.dbo.Soru SUBS 
								WHERE SUBS.ID_SORU = S.ID_SORU OR SUBS.ID_USTSORU = S.ID_SORU
								FOR JSON AUTO, INCLUDE_NULL_VALUES 
							) AS SoruPaketList
						)AS SoruPaketList
					FROM SoruBankasi.dbo.Soru S
					INNER JOIN SoruBankasi.dbo.SinavSoru SS ON SS.ID_SORU=S.ID_SORU
					WHERE ID_SINAV=@ID_SINAV AND SS.KITAPCIK='A' AND ID_SORUTUR<>1 AND ID_USTSORU IS NULL
					FOR JSON PATH, INCLUDE_NULL_VALUES
				) AS J
			END

			-- YENİ ONLINE SINAV

			IF @ISLEM = 8	--	ONLINE SINAV DETAY						
			BEGIN

				SELECT ISNULL((
					SELECT DISTINCT 
						 S.ID_SINAV
						,CEVAPGOR		= ISNULL(OSD.CEVAPGOR,0)
						,ACIKLANMATARIH	= CONVERT(varchar,ISNULL(OSD.ACIKLANMATARIH,DATEADD(WEEK,2,GETDATE())),104)
						,ACIKLANMASAAT	= CONVERT(char(5),ISNULL(OSD.ACIKLANMATARIH,DATEADD(WEEK,2,GETDATE())),108)
						,OSDO.ID_ONLINESINAVDETAYOTURUM
						,OTURUM			= ISNULL(SOD.OTURUM,1)
						,BASTARIH		= CONVERT(varchar,ISNULL(OSDO.BASTARIH,GETDATE()),104)
						,BASSAAT		= CONVERT(char(5),ISNULL(OSDO.BASTARIH,DATEADD(HOUR,ISNULL(SOD.OTURUM,1),GETDATE())),108)
						,BITTARIH		= CONVERT(varchar,ISNULL(OSDO.BITTARIH,DATEADD(WEEK,1,GETDATE())),104)
						,BITSAAT		= CONVERT(char(5),ISNULL(OSDO.BITTARIH,DATEADD(WEEK,1,DATEADD(HOUR,ISNULL(SOD.OTURUM,1),GETDATE()))),108)
						,SURE			= ISNULL(OSDO.SURE,100)
					FROM Sinav								S
					LEFT JOIN Tbl_OnlineSinavDetay			OSD	 ON	 OSD.ID_SINAV				= S.ID_SINAV
																 AND OSD.AKTIF					= 1 
					LEFT JOIN SinavDers						SD	 ON	 SD.ID_SINAV				= S.ID_SINAV
					LEFT JOIN SinavOptikDers				SOD	 ON	 SOD.ID_SINAVDERS			= SD.ID_SINAVDERS
					LEFT JOIN Tbl_OnlineSinavDetayOturum	OSDO ON	 OSDO.ID_ONLINESINAVDETAY	= OSD.ID_ONLINESINAVDETAY
																 AND OSDO.OTURUM				= SOD.OTURUM
					WHERE	S.ID_SINAV	= @ID_SINAV
					FOR JSON AUTO, INCLUDE_NULL_VALUES
				),'[]')

			END

			IF @ISLEM = 9	--	ONLINE SINAV DETAY KAYDET				
			BEGIN

				UPDATE Tbl_OnlineSinavDetay SET
					AKTIF = 0
				WHERE	ID_SINAV = JSON_VALUE (@SQLJSON, '$.ID_SINAV') 
					AND AKTIF	 = 1

				
				DROP TABLE IF EXISTS #TEMP9

				SELECT	 ID_SINAV
						,BASTARIH		= CAST(CAST(BASTARIH		+ ' ' +BASSAAT		+':00' AS datetime2) AS smalldatetime)
						,BITTARIH		= CAST(CAST(BITTARIH		+ ' ' +BITSAAT		+':00' AS datetime2) AS smalldatetime)
						,ACIKLANMATARIH = CAST(CAST(ACIKLANMATARIH	+ ' ' +ACIKLANMASAAT+':00' AS datetime2) AS smalldatetime)
						,SURE
						,CEVAPGOR
						,OTURUM
				INTO #TEMP9
				FROM OPENJSON(@SQLJSON)
				WITH(
					ID_SINAV		INT,
					CEVAPGOR		BIT,
					ACIKLANMATARIH	VARCHAR(MAX),
					ACIKLANMASAAT	VARCHAR(MAX),
					OSDO			NVARCHAR(MAX) AS JSON
				) AS J
				CROSS APPLY OPENJSON(J.OSDO)
				WITH(
					OTURUM		INT,
					BASTARIH	VARCHAR(MAX),
					BASSAAT		VARCHAR(MAX),
					BITTARIH	VARCHAR(MAX),
					BITSAAT		VARCHAR(MAX),
					SURE		INT		
				)

				DECLARE @INSTABLE9 TABLE (ID INT)

				
				INSERT INTO Tbl_OnlineSinavDetay (ID_SINAV, ACIKLANMATARIH, CEVAPGOR, KYE_TCKIMLIKNO)
				OUTPUT inserted.ID_ONLINESINAVDETAY  INTO @INSTABLE9 (ID)
				SELECT TOP 1 ID_SINAV, ACIKLANMATARIH, CEVAPGOR, @TCKIMLIKNO FROM #TEMP9
				
				DECLARE @ID_ONLINESINAVDETAY INT = (SELECT TOP 1 ID FROM @INSTABLE9)
				
				INSERT INTO Tbl_OnlineSinavDetayOturum (ID_ONLINESINAVDETAY, OTURUM, BASTARIH, BITTARIH, SURE)
				SELECT @ID_ONLINESINAVDETAY, OTURUM, BASTARIH, BITTARIH, SURE FROM #TEMP9

				DROP TABLE IF EXISTS #TEMP9

			END

			IF @ISLEM = 10	--	OGRENCI ATAMA							
			BEGIN

				IF EXISTS (SELECT TOP 1 1 FROM OPENJSON(@TC_OGRENCILIST))
				BEGIN
					INSERT INTO Tbl_OnlineSinavOgrenci (ID_SINAV, TCKIMLIKNO, KYE_TCKIMLIKNO)
					SELECT DISTINCT @ID_SINAV, VALUE AS TCKIMLIKNO, @TCKIMLIKNO FROM OPENJSON(@TC_OGRENCILIST) J
					WHERE NOT EXISTS (SELECT TOP 1 1 FROM Tbl_OnlineSinavOgrenci OS WHERE OS.AKTIF = 1 AND OS.ID_SINAV = @ID_SINAV AND OS.TCKIMLIKNO = J.VALUE)
				END
				ELSE
				BEGIN
					INSERT INTO Tbl_OnlineSinavOgrenci (ID_SINAV, TCKIMLIKNO, KYE_TCKIMLIKNO)
					SELECT DISTINCT @ID_SINAV, SO.TCKIMLIKNO, @TCKIMLIKNO
					FROM OPENJSON(@ID_SINIFLIST)		S
					JOIN OkyanusDB.dbo.vw_SinifOgrenci	SO	ON	SO.ID_SINIF	= S.VALUE
					WHERE NOT EXISTS (SELECT TOP 1 1 FROM Tbl_OnlineSinavOgrenci OS WHERE OS.AKTIF = 1 AND OS.ID_SINAV = @ID_SINAV AND OS.TCKIMLIKNO = SO.TCKIMLIKNO)
				END

			END

			IF @ISLEM = 11	--	OGRENCI ATAMA LİSTE						
			BEGIN

				SELECT ISNULL((
					SELECT DISTINCT 
						 K.TCKIMLIKNO
						,ADSOYAD		= K.AD + ' ' + K.SOYAD
						,TARIH			= CONVERT(VARCHAR, OSO.KYE_TARIH, 104 )
						,OGRENCISAYISI	= COUNT(1)
						,OGRENCILIST	= ISNULL((
							SELECT DISTINCT 
								 SK.TCKIMLIKNO
								,ADSOYAD	= SK.AD + ' ' + SK.SOYAD
								,SK.SUBEAD
								,SK.SINIFAD
							FROM Tbl_OnlineSinavOgrenci				SO
							JOIN OkyanusDB.dbo.vw_OgrenciDetayTum	SK	ON	SK.TCKIMLIKNO	= SO.TCKIMLIKNO
							WHERE	SO.KYE_TCKIMLIKNO	= K.TCKIMLIKNO
								AND SO.KYE_TARIH		= OSO.KYE_TARIH
								AND	SO.AKTIF			= 1
								AND SK.ID_SINIF			> 0
								AND SK.DONEM			= (SELECT OkyanusDB.dbo.fn_AktifDonem())
							FOR JSON PATH
						),'[]')
						,OSO.KYE_TARIH
					FROM Tbl_OnlineSinavOgrenci	OSO
					JOIN v3Kullanici			K	ON	K.TCKIMLIKNO	= OSO.KYE_TCKIMLIKNO
					WHERE	OSO.ID_SINAV	= @ID_SINAV
						AND OSO.AKTIF		= 1
						AND K.AKTIF			= 1
					GROUP BY OSO.KYE_TARIH, K.TCKIMLIKNO, K.AD, K.SOYAD
					FOR JSON PATH, INCLUDE_NULL_VALUES
				),'[]')

			END

			IF @ISLEM = 12	--	ÖĞRENCİ YENİ ONLINE SINAV LİSTESİ		
			BEGIN

				SELECT ISNULL((
					SELECT DISTINCT
						 S.ID_SINAV
						,SINAVTURU	= ST.AD
						,SINAVAD	= S.AD
						,SDO.OTURUM
						,ACIKLANDI	= CAST(IIF( (GETDATE() > SD.ACIKLANMATARIH AND S.DURUM = 2), 1 , 0) AS bit )
						,DEVAM		= CAST(CASE WHEN	GETDATE() BETWEEN SDO.BASTARIH AND SDO.BITTARIH 
													AND GETDATE() < DATEADD(MINUTE, SDO.SURE, ISNULL(SOO.BASTARIH,GETDATE())) THEN 1 
												ELSE 0 
											END 
										AS BIT)
						,COZULDU	= CAST( IIF(SOO.BASTARIH IS NULL, 0 , 1) AS bit)
						,PERFORMANS	= CASE 
										WHEN GETDATE() BETWEEN SDO.BASTARIH AND SDO.BITTARIH	THEN '--' 
										WHEN GETDATE() < SDO.BASTARIH							THEN 'Sınav Henüz Başlamadı' 
										WHEN GETDATE() > SD.ACIKLANMATARIH  AND S.DURUM != 2	THEN 'Açıklanması Bekleniyor' 
										ELSE CAST((	SELECT CAST(CAST(SOT.TN AS decimal(8,2)) / CAST((SOT.TD + SOT.TY + SOT.TB) AS decimal(8,2)) * 100.0  AS decimal(8,2))
													FROM SinavOgrenciToplam SOT
													WHERE	SOT.TCKIMLIKNO	= SO.TCKIMLIKNO
														AND	SOT.ID_SINAV	= S.ID_SINAV
											) AS VARCHAR)
									END
						,SDO.BITTARIH
					FROM Sinav								S
					JOIN Tbl_OnlineSinavDetay				SD	ON	SD.ID_SINAV				= S.ID_SINAV
					JOIN Tbl_OnlineSinavDetayOturum			SDO	ON	SDO.ID_ONLINESINAVDETAY	= SD.ID_ONLINESINAVDETAY 
																AND SDO.OTURUM				= IIF((GETDATE() > SD.ACIKLANMATARIH  AND S.DURUM = 2),  1, SDO.OTURUM)
					JOIN SinavTuru							ST	ON	ST.ID_SINAVTURU			= S.ID_SINAVTURU
					JOIN Tbl_OnlineSinavOgrenci				SO	ON	SO.ID_SINAV				= SD.ID_SINAV
					LEFT JOIN Tbl_OnlineSinavOgrenciOturum	SOO	ON	SOO.TCKIMLIKNO			= SO.TCKIMLIKNO
																AND SOO.ID_SINAV			= S.ID_SINAV
																AND SOO.OTURUM				= SDO.OTURUM

					WHERE	SO.TCKIMLIKNO	= @TCKIMLIKNO
						AND SD.AKTIF		= 1
						AND SO.AKTIF		= 1
					ORDER BY SDO.BITTARIH DESC
					FOR JSON PATH
				),'[]')

			END

			IF @ISLEM = 13	--	ÖĞRENCİ YENİ ONLINE SINAV DETAY			
			BEGIN
							
				--SELECT ISNULL((
				--	SELECT
				--		 S.ID_SINAV
				--		,SINAVAD	= S.AD
				--		,DERSLIST	= (
				--			SELECT D.BOLUMNO
				--				,D.ID_SINAVDERS
				--				,D.TAKMAAD
				--				,SORULIST.ID_SINAVANAHTAR
				--				,SORULIST.SORUNO
				--				,SORU = JSON_QUERY((
				--					SELECT SBS.ID_SORU
				--						,SORUHTML = 'SoruBankasi/' + SBSH.SORUHTML
				--						,CEVAPLIST = (
				--							SELECT 
				--								 CEVAPHTML = 'SoruBankasi/' + C.CEVAPHTML
				--								,C.DEGER
				--							FROM SoruBankasi.dbo.Cevap	C
				--							WHERE C.ID_SORU	= SBS.ID_SORU
				--							ORDER BY C.CEVAPNO
				--							FOR JSON PATH, INCLUDE_NULL_VALUES
				--						)
				--						,OGRENCICEVAP	= (
				--							SELECT CEVAP
				--							FROM Tbl_OnlineSinavOgrenciCevap	SOC	
				--							WHERE SOC.ID_ONLINESINAVOGRENCI = SO.ID_ONLINESINAVOGRENCI		
				--								AND SOC.ID_SINAVANAHTAR		= SORULIST.ID_SINAVANAHTAR
				--								AND SOC.AKTIF				= 1
				--							FOR JSON PATH, INCLUDE_NULL_VALUES
				--						)
				--					FROM SoruBankasi.dbo.Soru		SBS
				--					JOIN SoruBankasi.dbo.SoruDetay	SBSD	ON SBSD.ID_SORU	= SBS.ID_SORU
				--					JOIN SoruBankasi.dbo.SoruHtml	SBSH	ON SBSH.ID_SORU = SBSD.ID_SORU
				--					WHERE	SBS.ID_SORU	= SORULIST.ID_SORUBANKASI
				--						AND SBSD.SILINDI= 0						
				--					FOR JSON PATH, INCLUDE_NULL_VALUES, WITHOUT_ARRAY_WRAPPER
				--				))
				--			FROM SinavDers		D
				--			JOIN SinavAnahtar	SORULIST	ON	SORULIST.ID_SINAVDERS	= D.ID_SINAVDERS	
				--			JOIN SinavKitapcik	SK			ON	SK.ID_SINAVKITAPCIK		= SORULIST.ID_SINAVKITAPCIK
				--			WHERE	D.ID_SINAV	= S.ID_SINAV
				--				AND SK.AD		= 'A'
				--			ORDER BY SORULIST.SORUNO
				--			FOR JSON AUTO, INCLUDE_NULL_VALUES
				--		)
				--	FROM Sinav							S
				--	JOIN Tbl_OnlineSinavDetay			SD	ON	SD.ID_SINAV	= S.ID_SINAV
				--	JOIN Tbl_OnlineSinavOgrenci			SO	ON	SO.ID_SINAV	= SD.ID_SINAV
				--	WHERE	SO.TCKIMLIKNO	= @TCKIMLIKNO
				--		AND S.ID_SINAV		= @ID_SINAV
				--		AND SD.AKTIF		= 1
				--		AND SO.AKTIF		= 1
				--	FOR JSON PATH, INCLUDE_NULL_VALUES
				--),'[]')

				IF @SINAVOTURUM IS NULL 
					SET @SINAVOTURUM = 0

				IF	@SINAVOTURUM > 0
					AND NOT EXISTS (SELECT TOP 1 1 
									FROM Tbl_OnlineSinavOgrenci			O
									JOIN Tbl_OnlineSinavOgrenciOturum	OO	ON	OO.TCKIMLIKNO	= O.TCKIMLIKNO
									WHERE	O.TCKIMLIKNO	= @TCKIMLIKNO
										AND O.AKTIF			= 1
										AND OO.ID_SINAV		= @ID_SINAV
										AND OO.OTURUM		= @SINAVOTURUM
									)
					AND EXISTS (SELECT TOP 1 1
								FROM Tbl_OnlineSinavDetay		D
								JOIN Tbl_OnlineSinavDetayOturum	DO	ON	DO.ID_ONLINESINAVDETAY	= D.ID_ONLINESINAVDETAY
								WHERE	D.ID_SINAV	= @ID_SINAV
									AND D.AKTIF		= 1
									AND DO.OTURUM	= @SINAVOTURUM
									AND GETDATE() BETWEEN DO.BASTARIH AND DO.BITTARIH
								)
				BEGIN
					INSERT INTO  Tbl_OnlineSinavOgrenciOturum (TCKIMLIKNO, ID_SINAV, OTURUM )
					VALUES (@TCKIMLIKNO, @ID_SINAV, @SINAVOTURUM)
				END 


				--UPDATE Tbl_OnlineSinavOgrenci SET
				--	BASTARIH = GETDATE()
				--WHERE	ID_SINAV	= @ID_SINAV
				--	AND TCKIMLIKNO	= @TCKIMLIKNO
				--	AND AKTIF		= 1
				--	AND BASTARIH IS NULL

				SELECT ISNULL((
					SELECT DISTINCT
						 S.ID_SINAV
						,DEVAM			= CAST(CASE WHEN GETDATE() BETWEEN OSDO.BASTARIH AND OSDO.BITTARIH AND GETDATE() < DATEADD(MINUTE, OSDO.SURE, OSOO.BASTARIH) THEN 1 ELSE 0 END AS bit )
						,ACIKLANDI		= CAST(CASE WHEN GETDATE() > OSD.ACIKLANMATARIH THEN 1 ELSE 0 END AS bit )
						,KALANSURE		= CAST((DATEDIFF(SECOND,GETDATE(), DATEADD(MINUTE, OSDO.SURE, OSOO.BASTARIH)) / 60) AS varchar) + ':' + CAST((DATEDIFF(SECOND,GETDATE(), DATEADD(MINUTE, OSDO.SURE, OSOO.BASTARIH)) % 60) AS varchar)
						--,KALANSURE		= CAST(DATEDIFF(MINUTE,GETDATE(), DATEADD(MINUTE, OSDO.SURE, OSOO.BASTARIH)) AS varchar) + ':' + CAST((DATEDIFF(SECOND,GETDATE(), DATEADD(MINUTE, OSDO.SURE, OSOO.BASTARIH)) % 60) AS varchar)
						,SD.ID_SINAVDERS
						,BOLUMNO		= DENSE_RANK() OVER( ORDER BY SD.BOLUMNO)
						,SD.TAKMAAD
						,SA.SORUNO
						,SA.ID_SINAVANAHTAR
						,DOGRUCEVAP		= IIF(GETDATE() > OSD.ACIKLANMATARIH, SBDC.ID_CEVAP, NULL)
						--,DOGRUCEVAP	= CASE WHEN GETDATE() BETWEEN OSD.BASTARIH AND OSD.BITTARIH OR OSD.CEVAPGOR = 0 THEN NULL ELSE SBDC.ID_CEVAP END
						,SORUHTML		= CASE WHEN SBSH.SORUHTML IS NULL THEN '' ELSE 'SoruBankasi/'+ SBSH.SORUHTML END
						,OGRENCICEVAP	= ISNULL(OSOC.ID_CEVAP, NULL)
						,COZUMLIST		= JSON_QUERY(
											IIF(GETDATE() < OSD.ACIKLANMATARIH, NULL, (	SELECT	 SC.ID_COZUM
																								,COZUMHTML	= CASE WHEN SC.COZUMHTML  IS NULL THEN '' ELSE 'SoruBankasi/'+ SC.COZUMHTML  END
																						FROM SoruBankasi.dbo.SoruCozum SC
																						WHERE SC.ID_SORU = SBSH.ID_SORU
																						ORDER BY SC.SIRA
																						FOR JSON PATH)
											))
						,SBC.CEVAPNO
						,SBC.ID_CEVAP
						,CEVAPHTML		= CASE WHEN SBC.CEVAPHTML IS NULL THEN '' ELSE 'SoruBankasi/'+ SBC.CEVAPHTML END
						,DEGER			= CAST(IIF(GETDATE() > OSD.ACIKLANMATARIH, SBC.DEGER, NULL) AS BIT)
						--,DEGER		= CAST(CASE WHEN GETDATE() BETWEEN OSD.BASTARIH AND OSD.BITTARIH OR OSD.CEVAPGOR = 0 THEN NULL ELSE SBC.DEGER END AS bit)
						,OGRENCICEVAP	= CAST(IIF( OSOC.ID_CEVAP = SBC.ID_CEVAP , 1, 0) AS bit)
					FROM Sinav								S
					JOIN Tbl_OnlineSinavDetay				OSD		ON	OSD.ID_SINAV				= S.ID_SINAV
					JOIN Tbl_OnlineSinavDetayOturum			OSDO	ON	OSDO.ID_ONLINESINAVDETAY	= OSD.ID_ONLINESINAVDETAY
																	AND (	OSDO.OTURUM				= @SINAVOTURUM
																		OR	0						= @SINAVOTURUM)
					JOIN SinavKitapcik						SK		ON	SK.ID_SINAV					= S.ID_SINAV
					JOIN SinavDers							SD		ON	SD.ID_SINAV					= S.ID_SINAV
					JOIN SinavOptikDers						SOD		ON	SOD.ID_SINAVDERS			= SD.ID_SINAVDERS 
																	AND SOD.OTURUM					= OSDO.OTURUM
					JOIN SinavAnahtar						SA		ON	SA.ID_SINAVDERS				= SD.ID_SINAVDERS 
																	AND SA.ID_SINAVKITAPCIK			= SK.ID_SINAVKITAPCIK
					JOIN SoruBankasi.dbo.SoruHtml			SBSH	ON	SBSH.ID_SORU				= SA.ID_SORUBANKASI
					JOIN SoruBankasi.dbo.Cevap				SBC		ON	SBC.ID_SORU					= SBSH.ID_SORU
					JOIN SoruBankasi.dbo.Cevap				SBDC	ON	SBDC.ID_SORU				= SBSH.ID_SORU
																	AND SBDC.DEGER					= '1'
					JOIN Tbl_OnlineSinavOgrenci				OSO		ON	OSO.ID_SINAV				= S.ID_SINAV
					LEFT JOIN Tbl_OnlineSinavOgrenciOturum	OSOO	ON	OSOO.TCKIMLIKNO				= OSO.TCKIMLIKNO
																	AND OSOO.ID_SINAV				= S.ID_SINAV
																	AND OSOO.OTURUM					= OSDO.OTURUM
					LEFT JOIN Tbl_OnlineSinavOgrenciCevap	OSOC	ON	OSOC.ID_ONLINESINAVOGRENCI	= OSO.ID_ONLINESINAVOGRENCI
																	AND OSOC.ID_SINAVANAHTAR		= SA.ID_SINAVANAHTAR
																	AND OSOC.AKTIF					= 1
					WHERE	S.ID_SINAV		= @ID_SINAV
						AND OSO.TCKIMLIKNO	= @TCKIMLIKNO
						AND OSD.AKTIF		= 1
						AND OSO.AKTIF		= 1
						AND SK.AD			= 'A'
					ORDER BY BOLUMNO, SA.SORUNO, SBC.CEVAPNO
					FOR JSON AUTO, INCLUDE_NULL_VALUES
				),'[]')

				

			END

			IF @ISLEM = 14	--	ÖĞRENCİ YENİ ONLINE SINAV SORU İŞARETLE	
			BEGIN

				SET @ID_ONLINESINAVOGRENCI = ISNULL((
					SELECT TOP 1 ID_ONLINESINAVOGRENCI
					FROM Tbl_OnlineSinavOgrenci			O
					JOIN Tbl_OnlineSinavDetay			D	ON	D.ID_SINAV				= O.ID_SINAV
					JOIN Tbl_OnlineSinavDetayOturum		DO	ON	DO.ID_ONLINESINAVDETAY	= D.ID_ONLINESINAVDETAY
					JOIN Tbl_OnlineSinavOgrenciOturum	OO	ON	OO.TCKIMLIKNO			= O.TCKIMLIKNO
															AND OO.ID_SINAV				= O.ID_SINAV
															AND OO.OTURUM				= DO.OTURUM
					WHERE	O.TCKIMLIKNO	= @TCKIMLIKNO
						AND O.ID_SINAV		= @ID_SINAV
						AND DO.OTURUM		= @SINAVOTURUM
						AND O.AKTIF			= 1
						AND D.AKTIF			= 1
						AND GETDATE() BETWEEN DO.BASTARIH AND DO.BITTARIH
						AND GETDATE() < DATEADD(MINUTE, DO.SURE, OO.BASTARIH)
				),0)

				IF @ID_ONLINESINAVOGRENCI > 0
				BEGIN

					UPDATE Tbl_OnlineSinavOgrenciCevap SET
						AKTIF = 0
					WHERE	ID_ONLINESINAVOGRENCI	= @ID_ONLINESINAVOGRENCI
						AND	ID_SINAVANAHTAR			= @ID_SINAVANAHTAR
						AND AKTIF					= 1

					IF @ID_CEVAP IS NOT NULL
						INSERT INTO Tbl_OnlineSinavOgrenciCevap (ID_ONLINESINAVOGRENCI, ID_SINAVANAHTAR, ID_CEVAP)  
						VALUES (@ID_ONLINESINAVOGRENCI, @ID_SINAVANAHTAR, @ID_CEVAP)

				END

				SELECT ISNULL(@ID_ONLINESINAVOGRENCI,0)

			END

			IF @ISLEM = 15	--	ÖĞRENCİ YENİ ONLINE SINAV PERFORMANS	
			BEGIN
			
				DECLARE @TABLE TABLE(RESULT VARCHAR(MAX))

				INSERT INTO @TABLE
				EXEC sp_OnlineSinav @TCKIMLIKNO = @TCKIMLIKNO, @OTURUM = @OTURUM, @ID_SINAV = @ID_SINAV, @ISLEM = 13
								
				SELECT (
					SELECT ISNULL((
						SELECT DISTINCT
							 SINAVTURU		= ST.AD
							,SINAVAD		= S.AD
							,SONGIRIS		= (	SELECT TOP 1 CONVERT(varchar, OSOC.KYE_TARIH, 104) + ' ' + CONVERT(varchar, OSOC.KYE_TARIH, 108)
												FROM Tbl_OnlineSinavOgrenci			OSO 
												JOIN Tbl_OnlineSinavOgrenciCevap	OSOC	ON	OSOC.ID_ONLINESINAVOGRENCI = OSO.ID_ONLINESINAVOGRENCI
												WHERE	OSO.AKTIF	= 1
													AND OSOC.AKTIF	= 1
												ORDER BY OSOC.ID_ONLINESINAVOGRENCICEVAP DESC
												)
							,DOGRU			= SOT.TD
							,YANLIS			= SOT.TY
							,BOS			= SOT.TB
							,NET			= SOT.TN
							,NETPERFORMANS	= CAST(CAST(SOT.TN AS decimal(8,2)) / CAST((SOT.TD + SOT.TY + SOT.TB) AS decimal(8,2)) * 100.0  AS decimal(8,2))
							--,DOGRUPERFORMANS	= CAST(CAST(SOT.TD AS decimal(8,2)) / CAST((SOT.TD + SOT.TY + SOT.TB) AS decimal(8,2)) * 100.0  AS decimal(8,2))
							--,YANLISPERFORMANS	= CAST(CAST(SOT.TY AS decimal(8,2)) / CAST((SOT.TD + SOT.TY + SOT.TB) AS decimal(8,2)) * 100.0  AS decimal(8,2))
							--,BOSPERFORMANS	= CAST(CAST(SOT.TB AS decimal(8,2)) / CAST((SOT.TD + SOT.TY + SOT.TB) AS decimal(8,2)) * 100.0  AS decimal(8,2))
							,DERSPERFORMANS	= JSON_QUERY(
													ISNULL((	SELECT	 SD.BOLUMNO
																	,SD.TAKMAAD
																	,YUZDENET
															FROM SinavDers				SD
															JOIN vw_SinavOgrenciBolum	SOB	ON	SOB.ID_SINAVDERS	= SD.ID_SINAVDERS
																							AND SOB.TCKIMLIKNO		= SOT.TCKIMLIKNO
															WHERE SD.ID_SINAV = S.ID_SINAV
															ORDER BY BOLUMNO
															FOR JSON PATH
													),'[]')
												)
						FROM Tbl_OnlineSinavDetay		OSD
						JOIN Tbl_OnlineSinavDetayOturum	DO	ON	DO.ID_ONLINESINAVDETAY	= OSD.ID_ONLINESINAVDETAY
						JOIN SinavOgrenciToplam			SOT	ON	SOT.ID_SINAV	= OSD.ID_SINAV
						JOIN Sinav						S	ON	S.ID_SINAV		= SOT.ID_SINAV
						JOIN SinavTuru					ST	ON	ST.ID_SINAVTURU	= S.ID_SINAVTURU
						WHERE	SOT.ID_SINAV		= @ID_SINAV
							AND SOT.TCKIMLIKNO		= @TCKIMLIKNO
							AND OSD.AKTIF			= 1
							AND DO.BITTARIH			< GETDATE()
							AND OSD.ACIKLANMATARIH	< GETDATE()
						FOR JSON PATH, INCLUDE_NULL_VALUES
					),'[]') AS PERFORMANS,
					JSON_QUERY((	
						SELECT RESULT 
						FROM @TABLE
					))  AS SINAV
					FOR JSON PATH
				)

			END
			
			IF @ISLEM = 16	--	ÖĞRENCİ ATAMA KALDIRMA					
			BEGIN

				UPDATE O SET
					AKTIF = 0
				FROM Tbl_OnlineSinavOgrenci	O
				WHERE	ID_SINAV		= @ID_SINAV
					AND	KYE_TCKIMLIKNO	= @TC_EKLEYEN
					AND KYE_TARIH		= @KYE_TARIH
					AND NOT EXISTS (SELECT TOP 1 1 FROM Tbl_OnlineSinavOgrenciCevap C WHERE C.ID_ONLINESINAVOGRENCI = O.ID_ONLINESINAVOGRENCI AND C.AKTIF = 1)

				SELECT COUNT(DISTINCT TCKIMLIKNO)
				FROM Tbl_OnlineSinavOgrenci			O
				JOIN Tbl_OnlineSinavOgrenciCevap	C	ON	C.ID_ONLINESINAVOGRENCI = O.ID_ONLINESINAVOGRENCI					
				WHERE	O.ID_SINAV		= @ID_SINAV
					AND	O.KYE_TCKIMLIKNO= @TC_EKLEYEN
					AND O.KYE_TARIH		= @KYE_TARIH
					AND O.AKTIF			= 1
					AND C.AKTIF			= 1

			END

		END

	
	END TRY
	BEGIN CATCH
		DECLARE @_ErrorSeverity INT;
		DECLARE @_ErrorState INT;

		SELECT 
			@_ErrorSeverity	= ERROR_SEVERITY(),
			@_ErrorState		= ERROR_STATE()
				
		DECLARE @_MSG VARCHAR(4000)
		SELECT @_MSG = ERROR_MESSAGE()

		EXEC [dbo].[sp_CustomRaiseError] @MESSAGE = @_MSG, @SEVERITY = @_ErrorSeverity, @STATE = @_ErrorState, @ID_LOG = @_ID_LOG, @ID_LOGTUR = 2
	END CATCH;
END

GO


USE [Pusulam]
GO
CREATE NONCLUSTERED INDEX IX_OnlineDersSinif_ID_SINIF_ID_GUN_DERSNO_AKTIF_KYE_TCKIMLIKNO_INC_DERSAD_LINK_SIFRE
ON [dbo].[OnlineDersSinif] ([ID_SINIF],[ID_GUN],[DERSNO],[AKTIF],[KYE_TCKIMLIKNO])
INCLUDE ([DERSAD],[LINK],[SIFRE])
GO

USE [Pusulam]
GO
/****** Object:  StoredProcedure [dbo].[sp_UyariSistemleri]    Script Date: 25.08.2020 17:14:01 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER procedure [dbo].[sp_UyariSistemleri]
	@ISLEM					INT				= NULL,
	@TCKIMLIKNO				VARCHAR(11)		= NULL,
	@OTURUM					VARCHAR(36)		= NULL,
	@ID_MENU				INT				= NULL,
	
	@ID_UYARI				INT				= NULL,
	@SQLJSON				NVARCHAR(MAX)	= NULL

AS
BEGIN
	
	
	DECLARE @PROCNAME VARCHAR(MAX) = (SELECT OBJECT_NAME(@@PROCID))
	DECLARE @LOGJSON VARCHAR(MAX)
	SET @LOGJSON = (
		SELECT	 ISLEM					= @ISLEM
				,TCKIMLIKNO				= @TCKIMLIKNO
				,OTURUM					= @OTURUM
				,ID_MENU				= @ID_MENU

				,ID_UYARI				= @ID_UYARI
				,SQLJSON				= @SQLJSON
		FOR JSON PATH, WITHOUT_ARRAY_WRAPPER	   
	)	

	DECLARE @_ID_LOG INT = 0

	BEGIN TRY    
		DECLARE @_DESTEK_KONTROL INT
		EXEC @_ID_LOG = dbo.sp_OturumKontrolMenuYetkiLog @OTURUM = @OTURUM, @TCKIMLIKNO = @TCKIMLIKNO, @ID_MENU = @ID_MENU, @LOGJSON = @LOGJSON, @ISLEM = @ISLEM, @PROSEDURADI = @PROCNAME
		

		IF @_ID_LOG > 0
		BEGIN
			
			IF @ISLEM = 1	--	UYARI EKLE/DÜZENLE	
			BEGIN
				
				SET @ID_UYARI = JSON_VALUE(@SQLJSON, '$.ID_UYARI')

				IF NOT EXISTS (	SELECT TOP 1 1 FROM Uyari U WHERE U.ID_UYARI = @ID_UYARI)
				BEGIN
					DECLARE @INSERTUYARI TABLE (ID INT)
					INSERT INTO Uyari (AD,ACIKLAMA,BASTARIH, BITTARIH ,METIN, KYE_TCKIMLIKNO)
					OUTPUT inserted.ID_UYARI INTO @INSERTUYARI(ID)
					SELECT	 AD
							,ACIKLAMA
							,BASTARIH	= ISNULL(BASTARIH,GETDATE())
							,BITTARIH	= ISNULL(BITTARIH, DATEADD(DAY,1,GETDATE()))
							,METIN
							,@TCKIMLIKNO
					FROM OPENJSON(@SQLJSON)
					WITH(	AD			VARCHAR(MAX),
							ACIKLAMA	VARCHAR(MAX),
							BASTARIH	DATE,
							BITTARIH	DATE,
							METIN		NVARCHAR(MAX)
					)

					INSERT INTO UyariKullaniciTipi (ID_UYARI, ID_KULLANICITIPI)
					SELECT (SELECT TOP 1 ID FROM @INSERTUYARI), J.VALUE
					FROM OPENJSON(@SQLJSON, '$.ID_KULLANICITIPILIST') AS J
				END
				ELSE
				BEGIN

					UPDATE U SET
						AD		 = J.AD,
						ACIKLAMA = J.ACIKLAMA,
						BASTARIH = J.BASTARIH,
						BITTARIH = J.BITTARIH,
						METIN	 = J.METIN
					FROM Uyari	U
					JOIN OPENJSON(@SQLJSON)
					WITH(	ID_UYARI	INT,
							AD			VARCHAR(MAX),
							ACIKLAMA	VARCHAR(MAX),
							BASTARIH	DATE,
							BITTARIH	DATE,
							METIN		NVARCHAR(MAX)
					) AS J			ON J.ID_UYARI	= U.ID_UYARI

					DELETE UyariKullaniciTipi WHERE ID_UYARI = @ID_UYARI

					INSERT INTO UyariKullaniciTipi (ID_UYARI, ID_KULLANICITIPI)
					SELECT @ID_UYARI, J.VALUE
					FROM OPENJSON(@SQLJSON, '$.ID_KULLANICITIPILIST') AS J

				END
				
			END

			IF @ISLEM = 2	--	UYARI DETAY			
			BEGIN

				SELECT ISNULL((
					SELECT	 U.ID_UYARI
							,U.AD
							,U.ACIKLAMA
							,U.METIN
							,U.BASTARIH
							,U.BITTARIH
							,UKT.ID_KULLANICITIPI
					FROM Uyari				U
					JOIN UyariKullaniciTipi	UKT	ON	UKT.ID_UYARI	= U.ID_UYARI
					WHERE U.ID_UYARI = @ID_UYARI
					FOR JSON AUTO
				),'[]')				

			END

			IF @ISLEM = 3	--	UYARI LİSTE			
			BEGIN

				SELECT ISNULL((
					SELECT	 U.ID_UYARI
							,U.AD
							,U.ACIKLAMA
							,BASTARIH = CONVERT(VARCHAR,U.BASTARIH,104)
							,BITTARIH = CONVERT(VARCHAR,U.BITTARIH,104)
							,KYE_TARIH = CONVERT(VARCHAR,U.KYE_TARIH,104)
							,ADSOYAD = K.AD + ' ' + K.SOYAD
							,U.AKTIF
							,KT.ID_KULLANICITIPI
							,KT.AD
					FROM Uyari				U
					JOIN UyariKullaniciTipi	UKT	ON	UKT.ID_UYARI		= U.ID_UYARI
					JOIN v3KullaniciTipi	KT	ON	KT.ID_KULLANICITIPI	= UKT.ID_KULLANICITIPI
					JOIN v3Kullanici		K	ON	K.TCKIMLIKNO		= U.KYE_TCKIMLIKNO
					--WHERE U.AKTIF	= 1
					ORDER BY U.KYE_TARIH DESC, KT.AD
					FOR JSON AUTO
				),'[]')				

			END

			IF @ISLEM = 4	--	UYARI AKTIF/PASIF	
			BEGIN

				UPDATE U SET
					AKTIF = J.AKTIF
				FROM Uyari	U
				JOIN OPENJSON(@SQLJSON) 
				WITH(	ID_UYARI	INT,
						AKTIF		BIT
				) J ON J.ID_UYARI = U.ID_UYARI

			END

			IF @ISLEM = 5	--	UYARI GOSTER		
			BEGIN

				SELECT ISNULL((
					SELECT U.ID_UYARI, U.METIN
					FROM Uyari								U
					JOIN UyariKullaniciTipi					UKT	ON	UKT.ID_UYARI		= U.ID_UYARI
					JOIN OkyanusDB.dbo.vw_KullaniciYetki	KY	ON	KY.ID_KULLANICITIPI	= UKT.ID_KULLANICITIPI
					WHERE	KY.TCKIMLIKNO	= @TCKIMLIKNO
						AND U.AKTIF			= 1
						AND U.BASTARIH		<  CAST(GETDATE() AS date)
						AND U.BITTARIH		>= CAST(GETDATE() AS date)
					FOR JSON PATH
				),'[]')

			END

			IF @ISLEM = 6	--	UYARI KULLANICI ONAY
			BEGIN
				
				IF EXISTS (SELECT TOP 1 1 FROM UyariKullanici WHERE ID_UYARI = @ID_UYARI AND TCKIMLIKNO = @TCKIMLIKNO)
					UPDATE UyariKullanici SET
						ONAY = 1
					WHERE	ID_UYARI	= @ID_UYARI
						AND TCKIMLIKNO	= @TCKIMLIKNO
				ELSE
					INSERT INTO UyariKullanici (ID_UYARI, ONAY, TCKIMLIKNO)
					VALUES (@ID_UYARI, 1, @TCKIMLIKNO )

			END

			IF @ISLEM = 7	--	UYARI RAPOR			
			BEGIN

				IF EXISTS (SELECT TOP 1 1 FROM Uyari U WHERE U.ID_UYARI = @ID_UYARI AND RAPORSON = 0)
				BEGIN

					INSERT INTO UyariKullanici (ID_UYARI, TCKIMLIKNO, ONAY)
					SELECT DISTINCT @ID_UYARI, KY.TCKIMLIKNO, 0
					FROM UyariKullaniciTipi					UKT
					JOIN OkyanusDB.DBO.vw_KullaniciYetki	KY	ON	KY.ID_KULLANICITIPI	= UKT.ID_KULLANICITIPI
					WHERE	NOT EXISTS (SELECT TOP 1 1 FROM UyariKullanici UK WHERE UK.ID_UYARI = UKT.ID_UYARI AND UK.TCKIMLIKNO = KY.TCKIMLIKNO)
						AND	UKT.ID_UYARI = @ID_UYARI						

				END

				SELECT DISTINCT
					 TCKIMLIKNO		= K.TCKIMLIKNO
					,AD				= K.AD
					,SOYAD			= K.SOYAD
					,ONAY_TARIH		= CONVERT(VARCHAR,UK.ONAY_TARIH,104)	
					,ONAY_SAAT		= CONVERT(VARCHAR,UK.ONAY_TARIH,108)
					,ONAY			= IIF(ONAY = 1 ,'Onaylandı' ,'Onaylanmadı')
					,EPOSTA			= (SELECT TOP 1 EPOSTA FROM OkyanusDB..v3KullaniciEposta KE WHERE KE.TCKIMLIKNO = K.TCKIMLIKNO)
					,SUBELIST		= (SELECT STUFF((	SELECT DISTINCT ', ' +  CAST( SB.AD AS VARCHAR) 
														FROM OkyanusDB.DBO.vw_KullaniciYetki KY 
														JOIN v3Sube							 SB ON SB.ID_SUBE = KY.ID_SUBE
														WHERE KY.TCKIMLIKNO = K.TCKIMLIKNO 
										FOR XML PATH(''), TYPE).value('.', 'VARCHAR(20)'), 1, 2, ''))
					,KULLANICITIPI	= (SELECT STUFF((	SELECT DISTINCT ', ' +  CAST( KT.AD AS VARCHAR) 
														FROM UyariKullaniciTipi				 UKT
														JOIN v3KullaniciTipi				 KT ON	KT.ID_KULLANICITIPI	= UKT.ID_KULLANICITIPI
														JOIN OkyanusDB.DBO.vw_KullaniciYetki KY ON	KY.ID_KULLANICITIPI = KT.ID_KULLANICITIPI
										FOR XML PATH(''), TYPE).value('.', 'VARCHAR(MAX)'), 1, 2, ''))
				FROM Uyari			U
				JOIN UyariKullanici	UK	ON	UK.ID_UYARI	= U.ID_UYARI
				JOIN v3Kullanici	K	ON	K.TCKIMLIKNO= UK.TCKIMLIKNO
				WHERE U.ID_UYARI = @ID_UYARI

				SELECT AD
				FROM Uyari 
				WHERE ID_UYARI = @ID_UYARI

			END

		END


	END TRY
	BEGIN CATCH
		DECLARE @_ErrorSeverity INT;
		DECLARE @_ErrorState INT;

		SELECT 
			@_ErrorSeverity	= ERROR_SEVERITY(),
			@_ErrorState		= ERROR_STATE()
				
		DECLARE @_MSG VARCHAR(4000)
		SELECT @_MSG = ERROR_MESSAGE()

		EXEC [dbo].[sp_CustomRaiseError] @MESSAGE = @_MSG, @SEVERITY = @_ErrorSeverity, @STATE = @_ErrorState, @ID_LOG = @_ID_LOG, @ID_LOGTUR = 2
	END CATCH;
END


