USE [Pusulam]
GO
/****** Object:  StoredProcedure [dbo].[sp_MorpaKullaniciListesi]    Script Date: 20.10.2020 14:32:04 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER procedure [dbo].[sp_MorpaKullaniciListesi]
	@ISLEM		INT				= NULL,
	@APIKEY		VARCHAR(30)		= NULL
AS
BEGIN
/*

	Değiştiren Adı		: Kadir Cengiz
	Değiştirilen Tarih	: 20.10.2020
	
	--	sp Create 
		--	ISLEM = 1 ADD
	--	MORPA KULLANICI LİSTE PAYLAŞIMI

*/	
	
	DECLARE @PROCNAME VARCHAR(MAX) = (SELECT OBJECT_NAME(@@PROCID))
	DECLARE @LOGJSON VARCHAR(MAX)
	SET @LOGJSON = (
		SELECT	 ISLEM		= @ISLEM	
				,APIKEY		= @APIKEY
		FOR JSON PATH, WITHOUT_ARRAY_WRAPPER	   
	)	
	
	BEGIN TRY    
		
		IF @APIKEY != '2ded8ba1-97a4-49b4-9a92-b70c0'
			SELECT 'Yanlış Api Key'
		ELSE IF	DATEPART(HOUR, GETDATE()) BETWEEN 7 AND 21
			SELECT 'Saat 22:00 ile 07:00 arasında kullanıma açıktır. Lütfen belirtilen saatlerde tekrar deneyiniz.'
			--RAISERROR ('Saat 22:00 ile 07:00 arasında kullanıma açıktır. Lütfen belirtilen saatlerde tekrar deneyiniz.',16,1)			
		ELSE
		BEGIN
			
			IF @ISLEM = 1	--	LİSTE
			BEGIN
				
				DROP TABLE IF EXISTS #ILKOKULVEANAOKUL
				DROP TABLE IF EXISTS #ORTAOKULLISE
				DROP TABLE IF EXISTS #OGRENCI
				
				SELECT	 TIPI			= 'OGRENCI'
						,KODU			= K.TCKIMLIKNO
						,ADI			= K.AD
						,SOYADI			= K.SOYAD
						,CINSIYET		= IIF(K.CINSIYET=1,'K','E')
						,KAMPUS_ID		= O.ID_SUBE
						,SINIF			= K3.DUZEY
						,SUNUFSUBE		= S.AD
						,EPOSTA			= IIF(KE.EPOSTA = '' OR KE.EPOSTA = '-', NULL, KE.EPOSTA)
						,SIFRE			= 1234--SUBSTRING(REVERSE(K.TCKIMLIKNO),1,6)
						,IPTALDURUMU	= IIF(K.AKTIF = 1, 0, 1)
						,SIRA			= ROW_NUMBER() OVER( PARTITION BY K.TCKIMLIKNO ORDER BY S.ID_SINIF)
				INTO #OGRENCI
				FROM v3Kullanici	K
				JOIN v3Ogrenci		O	ON	O.TCKIMLIKNO	= K.TCKIMLIKNO
				JOIN v3Sinif		S	ON	S.ID_SINIF		= O.ID_SINIF
				JOIN v3SatisTuru	ST	ON	ST.ID_SATISTURU	= S.ID_SATISTURU
				JOIN v3Kademe3		K3	ON	K3.ID_KADEME3	= ST.ID_KADEME3
				LEFT JOIN OkyanusDB..v3KullaniciEposta	KE	ON	KE.TCKIMLIKNO	= K.TCKIMLIKNO
				ORDER BY K.TCKIMLIKNO

				SELECT	 TIPI			= 'OGRETMEN'
						,KODU			= K.TCKIMLIKNO
						,ADI			= K.AD
						,SOYADI			= K.SOYAD
						,CINSIYET		= IIF(K.CINSIYET=1,'K','E')
						,KAMPUS_ID		= VS.ID_SUBE
						,SINIF			= K3.DUZEY
						,SUNUFSUBE		= VS.SINIF
						,EPOSTA			= IIF(KE.EPOSTA = '' OR KE.EPOSTA = '-', NULL, KE.EPOSTA)
						,SIFRE			= 1234--SUBSTRING(REVERSE(K.TCKIMLIKNO),1,6)
						,IPTALDURUMU	= IIF(K.AKTIF = 1, 0, 1)
						,BRANS			= O.BRANS
						,SIRA			= ROW_NUMBER() OVER( PARTITION BY K.TCKIMLIKNO ORDER BY SD.ID_SINIF)
				INTO #ILKOKULVEANAOKUL
				FROM v3Kullanici						K
				JOIN v3Ogretmen							O	ON	O.TCKIMLIKNO	= K.TCKIMLIKNO
				JOIN OkyanusDB..vw_SinifDanisman		SD	ON	SD.TCKIMLIKNO	= O.TCKIMLIKNO AND SD.ID_SINIF > 0
				JOIN OkyanusDB..vw_v4SinifDetay			VS	ON	VS.ID_SINIF		= SD.ID_SINIF AND VS.DONEM = OkyanusDB.dbo.fn_AktifDonem()
				JOIN v3Kademe3							K3	ON	K3.ID_KADEME3	= VS.ID_KADEME3
				LEFT JOIN OkyanusDB..v3KullaniciEposta	KE	ON	KE.TCKIMLIKNO	= K.TCKIMLIKNO
				WHERE VS.ID_KADEME < 4
				ORDER BY K.TCKIMLIKNO

				SELECT	 TIPI			= 'OGRETMEN'
						,KODU			= K.TCKIMLIKNO
						,ADI			= K.AD
						,SOYADI			= K.SOYAD
						,CINSIYET		= IIF(K.CINSIYET=1,'K','E')
						,KAMPUS_ID		= VS.ID_SUBE
						,SINIF			= K3.DUZEY
						,SUNUFSUBE		= VS.SINIF
						,EPOSTA			= IIF(KE.EPOSTA = '' OR KE.EPOSTA = '-', NULL, KE.EPOSTA)
						,SIFRE			= 1234--SUBSTRING(REVERSE(K.TCKIMLIKNO),1,6)
						,IPTALDURUMU	= IIF(K.AKTIF = 1, 0, 1)
						,BRANS			= O.BRANS
						,SIRA			= ROW_NUMBER() OVER( PARTITION BY K.TCKIMLIKNO ORDER BY SD.ID_SINIF)
				INTO #ORTAOKULLISE
				FROM v3Kullanici						K
				JOIN v3Ogretmen							O	ON	O.TCKIMLIKNO	= K.TCKIMLIKNO
				JOIN OkyanusDB..v3DersProgram			SD	ON	SD.TCKIMLIKNO	= O.TCKIMLIKNO	AND SD.ID_SINIF > 0
				JOIN OkyanusDB..vw_v4SinifDetay			VS	ON	VS.ID_SINIF		= SD.ID_SINIF	AND VS.DONEM = OkyanusDB.dbo.fn_AktifDonem()
				JOIN v3Kademe3							K3	ON	K3.ID_KADEME3	= VS.ID_KADEME3
				LEFT JOIN OkyanusDB..v3KullaniciEposta	KE	ON	KE.TCKIMLIKNO	= K.TCKIMLIKNO
				WHERE VS.ID_KADEME > 3
				ORDER BY K.TCKIMLIKNO



				;WITH LISTE AS(
				SELECT TIPI, KODU, ADI, SOYADI, CINSIYET, KAMPUS_ID, SINIF, SUNUFSUBE, EPOSTA, SIFRE, IPTALDURUMU, BRANS = NULL
				FROM #OGRENCI WHERE SIRA = 1
				UNION
				SELECT TIPI, KODU, ADI, SOYADI, CINSIYET, KAMPUS_ID, SINIF, SUNUFSUBE, EPOSTA, SIFRE, IPTALDURUMU, BRANS 
				FROM #ILKOKULVEANAOKUL WHERE SIRA = 1
				UNION
				SELECT TIPI, KODU, ADI, SOYADI, CINSIYET, KAMPUS_ID, SINIF, SUNUFSUBE, EPOSTA, SIFRE, IPTALDURUMU, BRANS 
				FROM #ORTAOKULLISE WHERE SIRA = 1)
				SELECT (
					SELECT (
						SELECT TIPI, KODU, ADI, SOYADI, CINSIYET, KAMPUS_ID, SINIF, SUNUFSUBE, EPOSTA, SIFRE, IPTALDURUMU, BRANS 
						FROM LISTE
						ORDER BY KODU
						FOR JSON PATH, INCLUDE_NULL_VALUES
					) AS LISTE,
					(
						SELECT TIPI, KODU, ADI, SOYADI, CINSIYET, KAMPUS_ID, SINIF, SUNUFSUBE, EPOSTA, SIFRE, IPTALDURUMU, BRANS 
						FROM #ILKOKULVEANAOKUL 
						WHERE SIRA > 1
						FOR JSON PATH, INCLUDE_NULL_VALUES
					) ILKOKULCOKLU
					FOR JSON PATH, INCLUDE_NULL_VALUES
				)
				
				
				DROP TABLE IF EXISTS #ILKOKULVEANAOKUL
				DROP TABLE IF EXISTS #ORTAOKULLISE
				DROP TABLE IF EXISTS #OGRENCI

			END

		END


	END TRY
	BEGIN CATCH
		DECLARE @_ErrorSeverity INT;
		DECLARE @_ErrorState INT;

		SELECT  @_ErrorSeverity	= ERROR_SEVERITY(),
				@_ErrorState	= ERROR_STATE()
				
		DECLARE @_MSG VARCHAR(4000)
		SELECT @_MSG = ERROR_MESSAGE()

		--EXEC [dbo].[sp_CustomRaiseError] @MESSAGE = @_MSG, @SEVERITY = @_ErrorSeverity, @STATE = @_ErrorState, @ID_LOG = @_ID_LOG, @ID_LOGTUR = 2
	END CATCH;
END
