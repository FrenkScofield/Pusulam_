USE [Pusulam]
GO

/*

	Değiştiren Adı		: Kadir Cengiz
	Değiştirilen Tarih	: 22.08.20202
	
	--	sp Create
	--	Uyarı Sistemleri 

*/

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE procedure [dbo].[sp_UyariSistemleri]
	@ISLEM					INT				= NULL,
	@TCKIMLIKNO				VARCHAR(11)		= NULL,
	@OTURUM					VARCHAR(36)		= NULL,
	@ID_MENU				INT				= NULL,
	
	@ID_UYARI				INT				= NULL,
	@SQLJSON				NVARCHAR(MAX)	= NULL

AS
BEGIN
	
	
	DECLARE @PROCNAME VARCHAR(MAX) = (SELECT OBJECT_NAME(@@PROCID))
	DECLARE @LOGJSON VARCHAR(MAX)
	SET @LOGJSON = (
		SELECT	 ISLEM					= @ISLEM
				,TCKIMLIKNO				= @TCKIMLIKNO
				,OTURUM					= @OTURUM
				,ID_MENU				= @ID_MENU

				,ID_UYARI				= @ID_UYARI
				,SQLJSON				= @SQLJSON
		FOR JSON PATH, WITHOUT_ARRAY_WRAPPER	   
	)	

	DECLARE @_ID_LOG INT = 0

	BEGIN TRY    
		DECLARE @_DESTEK_KONTROL INT
		EXEC @_ID_LOG = dbo.sp_OturumKontrolMenuYetkiLog @OTURUM = @OTURUM, @TCKIMLIKNO = @TCKIMLIKNO, @ID_MENU = @ID_MENU, @LOGJSON = @LOGJSON, @ISLEM = @ISLEM, @PROSEDURADI = @PROCNAME
		

		IF @_ID_LOG > 0
		BEGIN
			
			IF @ISLEM = 1	--	UYARI EKLE/DÜZENLE	
			BEGIN
				
				SET @ID_UYARI = JSON_VALUE(@SQLJSON, '$.ID_UYARI')

				IF NOT EXISTS (	SELECT TOP 1 1 FROM Uyari U WHERE U.ID_UYARI = @ID_UYARI)
				BEGIN
					DECLARE @INSERTUYARI TABLE (ID INT)
					INSERT INTO Uyari (AD,ACIKLAMA,BASTARIH, BITTARIH ,METIN, KYE_TCKIMLIKNO)
					OUTPUT inserted.ID_UYARI INTO @INSERTUYARI(ID)
					SELECT	 AD
							,ACIKLAMA
							,BASTARIH	= ISNULL(BASTARIH,GETDATE())
							,BITTARIH	= ISNULL(BITTARIH, DATEADD(DAY,1,GETDATE()))
							,METIN
							,@TCKIMLIKNO
					FROM OPENJSON(@SQLJSON)
					WITH(	AD			VARCHAR(MAX),
							ACIKLAMA	VARCHAR(MAX),
							BASTARIH	DATE,
							BITTARIH	DATE,
							METIN		NVARCHAR(MAX)
					)

					INSERT INTO UyariKullaniciTipi (ID_UYARI, ID_KULLANICITIPI)
					SELECT (SELECT TOP 1 ID FROM @INSERTUYARI), J.VALUE
					FROM OPENJSON(@SQLJSON, '$.ID_KULLANICITIPILIST') AS J
				END
				ELSE
				BEGIN

					UPDATE U SET
						AD		 = J.AD,
						ACIKLAMA = J.ACIKLAMA,
						BASTARIH = J.BASTARIH,
						BITTARIH = J.BITTARIH,
						METIN	 = J.METIN
					FROM Uyari	U
					JOIN OPENJSON(@SQLJSON)
					WITH(	ID_UYARI	INT,
							AD			VARCHAR(MAX),
							ACIKLAMA	VARCHAR(MAX),
							BASTARIH	DATE,
							BITTARIH	DATE,
							METIN		NVARCHAR(MAX)
					) AS J			ON J.ID_UYARI	= U.ID_UYARI

					DELETE UyariKullaniciTipi WHERE ID_UYARI = @ID_UYARI

					INSERT INTO UyariKullaniciTipi (ID_UYARI, ID_KULLANICITIPI)
					SELECT @ID_UYARI, J.VALUE
					FROM OPENJSON(@SQLJSON, '$.ID_KULLANICITIPILIST') AS J

				END
				
			END

			IF @ISLEM = 2	--	UYARI DETAY			
			BEGIN

				SELECT ISNULL((
					SELECT	 U.ID_UYARI
							,U.AD
							,U.ACIKLAMA
							,U.METIN
							,U.BASTARIH
							,U.BITTARIH
							,UKT.ID_KULLANICITIPI
					FROM Uyari				U
					JOIN UyariKullaniciTipi	UKT	ON	UKT.ID_UYARI	= U.ID_UYARI
					WHERE U.ID_UYARI = @ID_UYARI
					FOR JSON AUTO
				),'[]')				

			END

			IF @ISLEM = 3	--	UYARI LİSTE			
			BEGIN

				SELECT ISNULL((
					SELECT	 U.ID_UYARI
							,U.AD
							,U.ACIKLAMA
							,BASTARIH = CONVERT(VARCHAR,U.BASTARIH,104)
							,BITTARIH = CONVERT(VARCHAR,U.BITTARIH,104)
							,KYE_TARIH = CONVERT(VARCHAR,U.KYE_TARIH,104)
							,ADSOYAD = K.AD + ' ' + K.SOYAD
							,U.AKTIF
							,KT.ID_KULLANICITIPI
							,KT.AD
					FROM Uyari				U
					JOIN UyariKullaniciTipi	UKT	ON	UKT.ID_UYARI		= U.ID_UYARI
					JOIN v3KullaniciTipi	KT	ON	KT.ID_KULLANICITIPI	= UKT.ID_KULLANICITIPI
					JOIN v3Kullanici		K	ON	K.TCKIMLIKNO		= U.KYE_TCKIMLIKNO
					--WHERE U.AKTIF	= 1
					ORDER BY U.KYE_TARIH DESC, KT.AD
					FOR JSON AUTO
				),'[]')				

			END

			IF @ISLEM = 4	--	UYARI AKTIF/PASIF	
			BEGIN

				UPDATE U SET
					AKTIF = J.AKTIF
				FROM Uyari	U
				JOIN OPENJSON(@SQLJSON) 
				WITH(	ID_UYARI	INT,
						AKTIF		BIT
				) J ON J.ID_UYARI = U.ID_UYARI

			END

			IF @ISLEM = 5	--	UYARI GOSTER		
			BEGIN

				SELECT ISNULL((
					SELECT U.ID_UYARI, U.METIN
					FROM Uyari								U
					JOIN UyariKullaniciTipi					UKT	ON	UKT.ID_UYARI		= U.ID_UYARI
					JOIN OkyanusDB.dbo.vw_KullaniciYetki	KY	ON	KY.ID_KULLANICITIPI	= UKT.ID_KULLANICITIPI
					WHERE	KY.TCKIMLIKNO	= @TCKIMLIKNO
						AND U.AKTIF			= 1
						AND U.BASTARIH		<  CAST(GETDATE() AS date)
						AND U.BITTARIH		>= CAST(GETDATE() AS date)
					FOR JSON PATH
				),'[]')

			END

			IF @ISLEM = 6	--	UYARI KULLANICI ONAY
			BEGIN

				INSERT INTO UyariKullanici (ID_UYARI, ONAY, TCKIMLIKNO)
				VALUES (@ID_UYARI, 1, @TCKIMLIKNO )

			END

			IF @ISLEM = 7	--	UYARI RAPOR			
			BEGIN

				IF EXISTS (SELECT TOP 1 1 FROM Uyari U WHERE U.ID_UYARI = @ID_UYARI AND RAPORSON = 0)
				BEGIN

					INSERT INTO UyariKullanici (ID_UYARI, TCKIMLIKNO, ONAY)
					SELECT DISTINCT @ID_UYARI, KY.TCKIMLIKNO, 0
					FROM UyariKullaniciTipi					UKT
					JOIN OkyanusDB.DBO.vw_KullaniciYetki	KY	ON	KY.ID_KULLANICITIPI	= UKT.ID_KULLANICITIPI
					WHERE	NOT EXISTS (SELECT TOP 1 1 FROM UyariKullanici UK WHERE UK.ID_UYARI = UKT.ID_UYARI AND UK.TCKIMLIKNO = KY.TCKIMLIKNO)
						AND	UKT.ID_UYARI = @ID_UYARI						

				END

				SELECT	 UYARIAD		= U.AD
						,BASTARIH		= CONVERT(VARCHAR,U.BASTARIH,104)
						,BITTARIH		= CONVERT(VARCHAR,U.BITTARIH,104)
						,TCKIMLIKNO		= K.TCKIMLIKNO
						,AD				= K.AD
						,SOYAD			= K.SOYAD
						,ONAY_TARIH		= CONVERT(VARCHAR,UK.ONAY_TARIH,104)	
						,ONAY_SAAT		= CONVERT(VARCHAR,UK.ONAY_TARIH,108)
						,ONAY			= IIF(ONAY = 1 ,'Onaylandı' ,'Onaylanmadı')
						,KULLANICITIPI	= (SELECT STUFF((	SELECT DISTINCT ', ' +  CAST( KT.AD AS VARCHAR) 
															FROM OkyanusDB.DBO.vw_KullaniciYetki KY 
															JOIN v3KullaniciTipi				 KT ON KT.ID_KULLANICITIPI	= KY.ID_KULLANICITIPI
															WHERE KY.TCKIMLIKNO = K.TCKIMLIKNO 
										 FOR XML PATH(''), TYPE).value('.', 'VARCHAR(MAX)'), 1, 2, ''))
				FROM Uyari			U
				JOIN UyariKullanici	UK	ON	UK.ID_UYARI	= U.ID_UYARI
				JOIN v3Kullanici	K	ON	K.TCKIMLIKNO= UK.TCKIMLIKNO
				WHERE U.ID_UYARI = @ID_UYARI

			END

		END


	END TRY
	BEGIN CATCH
		DECLARE @_ErrorSeverity INT;
		DECLARE @_ErrorState INT;

		SELECT 
			@_ErrorSeverity	= ERROR_SEVERITY(),
			@_ErrorState		= ERROR_STATE()
				
		DECLARE @_MSG VARCHAR(4000)
		SELECT @_MSG = ERROR_MESSAGE()

		EXEC [dbo].[sp_CustomRaiseError] @MESSAGE = @_MSG, @SEVERITY = @_ErrorSeverity, @STATE = @_ErrorState, @ID_LOG = @_ID_LOG, @ID_LOGTUR = 2
	END CATCH;
END


GO

INSERT INTO Menu (AD, ACIKLAMA, KOD, URL, RESIM)
VALUES ('Uyarı Sistemleri İşlemleri', 'Uyarı Sistemleri Ekle Düzelt', '047004','#Yonetim/UyariSistemiIslemleri', 'icon-pencil')

INSERT INTO Menu (AD, ACIKLAMA, KOD, URL, RESIM)
VALUES ('Uyarı Sistemleri Listesi', 'Uyarı Sistemleri Liste Aktif Sil', '047005','#Yonetim/UyariSistemiListe', 'icon-list')
GO

USE [Pusulam]
GO

/****** Object:  Table [dbo].[Uyari]    Script Date: 24.08.2020 15:05:53 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE TABLE [dbo].[Uyari](
	[ID_UYARI] [int] IDENTITY(1,1) NOT NULL,
	[AD] [nvarchar](max) NOT NULL,
	[ACIKLAMA] [varchar](max) NULL,
	[METIN] [nvarchar](max) NOT NULL,
	[BASTARIH] [date] NOT NULL,
	[BITTARIH] [date] NOT NULL,
	[AKTIF] [bit] NOT NULL,
	[RAPORSON] [bit] NOT NULL,
	[KYE_TCKIMLIKNO] [varchar](11) NOT NULL,
	[KYE_TARIH] [datetime] NOT NULL,
 CONSTRAINT [PK_Uyari] PRIMARY KEY CLUSTERED 
(
	[ID_UYARI] ASC
)WITH (PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON) ON [PRIMARY]
) ON [PRIMARY] TEXTIMAGE_ON [PRIMARY]
GO

ALTER TABLE [dbo].[Uyari] ADD  CONSTRAINT [DF_Uyari_BASTARIH]  DEFAULT (getdate()) FOR [BASTARIH]
GO

ALTER TABLE [dbo].[Uyari] ADD  CONSTRAINT [DF_Uyari_BITTARIH]  DEFAULT (dateadd(day,(1),getdate())) FOR [BITTARIH]
GO

ALTER TABLE [dbo].[Uyari] ADD  CONSTRAINT [DF_Uyari_AKTIF]  DEFAULT ((1)) FOR [AKTIF]
GO

ALTER TABLE [dbo].[Uyari] ADD  CONSTRAINT [DF_Uyari_RAPORSON]  DEFAULT ((0)) FOR [RAPORSON]
GO

ALTER TABLE [dbo].[Uyari] ADD  CONSTRAINT [DF_Uyari_KYE_TARIH]  DEFAULT (getdate()) FOR [KYE_TARIH]
GO




USE [Pusulam]
GO

/****** Object:  Table [dbo].[UyariKullanici]    Script Date: 24.08.2020 15:05:57 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE TABLE [dbo].[UyariKullanici](
	[ID_UYARIKULLANICI] [int] IDENTITY(1,1) NOT NULL,
	[ID_UYARI] [int] NOT NULL,
	[TCKIMLIKNO] [varchar](11) NOT NULL,
	[ONAY] [bit] NOT NULL,
	[ONAY_TARIH]  AS (case when [ONAY]=(1) then getdate()  end),
 CONSTRAINT [PK_UyariKullanici] PRIMARY KEY CLUSTERED 
(
	[ID_UYARIKULLANICI] ASC
)WITH (PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON) ON [PRIMARY]
) ON [PRIMARY]
GO

ALTER TABLE [dbo].[UyariKullanici] ADD  CONSTRAINT [DF_UyariKullanici_ONAY]  DEFAULT ((0)) FOR [ONAY]
GO

ALTER TABLE [dbo].[UyariKullanici]  WITH CHECK ADD  CONSTRAINT [FK_UyariKullanici_Uyari] FOREIGN KEY([ID_UYARI])
REFERENCES [dbo].[Uyari] ([ID_UYARI])
GO

ALTER TABLE [dbo].[UyariKullanici] CHECK CONSTRAINT [FK_UyariKullanici_Uyari]
GO




USE [Pusulam]
GO

/****** Object:  Table [dbo].[UyariKullaniciTipi]    Script Date: 24.08.2020 15:06:04 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE TABLE [dbo].[UyariKullaniciTipi](
	[ID_UYARIKULLANICITIPI] [int] IDENTITY(1,1) NOT NULL,
	[ID_UYARI] [int] NOT NULL,
	[ID_KULLANICITIPI] [int] NOT NULL,
	[AKTIF] [bit] NOT NULL,
 CONSTRAINT [PK_UyariKullaniciTipi] PRIMARY KEY CLUSTERED 
(
	[ID_UYARIKULLANICITIPI] ASC
)WITH (PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON) ON [PRIMARY]
) ON [PRIMARY]
GO

ALTER TABLE [dbo].[UyariKullaniciTipi] ADD  CONSTRAINT [DF_UyariKullaniciTipi_AKTIF]  DEFAULT ((1)) FOR [AKTIF]
GO

ALTER TABLE [dbo].[UyariKullaniciTipi]  WITH CHECK ADD  CONSTRAINT [FK_UyariKullaniciTipi_Uyari] FOREIGN KEY([ID_UYARI])
REFERENCES [dbo].[Uyari] ([ID_UYARI])
GO

ALTER TABLE [dbo].[UyariKullaniciTipi] CHECK CONSTRAINT [FK_UyariKullaniciTipi_Uyari]
GO




GO

USE [OkyanusDB]
GO
CREATE NONCLUSTERED INDEX IX_v4SinifOgrenci_AKTIF_TC_OGRENCI_INC_ID_SINIF
ON [dbo].[v4SinifOgrenci] ([AKTIF],[TC_OGRENCI])
INCLUDE ([ID_SINIF])
GO



USE [eokul_v2]
GO
/*

	Değiştiren Adı		: Kadir Cengiz
	Değiştirilen Tarih	: 24.08.20202
	
	--	sp alter 
			--	ISLEM 17 ADD
			--	ISLEM 18 ADD
	--	Uyarı Sistemleri EKLENDİ

*/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[spApi_Anasayfa]
(
	@TCKIMLIKNO			VARCHAR(11)		= NULL,
	@OTURUM				VARCHAR(MAX)	= NULL,
	@ISLEM				INT				= NULL,

	@ID_ICERIK			INT				= NULL,
	@TCKIMLIKNO_LOGIN	VARCHAR(11)		= NULL,
	@TCGIRISYAPAN		VARCHAR(11)		= NULL,
	@OTURUMGIRISYAPAN	VARCHAR(MAX)	= NULL,
	@SAYFA				VARCHAR(MAX)	= NULL,
	@SORUN				VARCHAR(MAX)	= NULL,
	@ID_KULLANICITIP	INT				= NULL,
	@ID_KULLANICI		INT				= NULL,
	@EMAIL				VARCHAR(MAX)	= NULL,
	@ILETISIMTURU		VARCHAR(MAX)	= NULL,
	@TELEFON			VARCHAR(MAX)	= NULL,
	@ID_OGRENCI			INT				= NULL,
	@ID_ODEV			INT				= NULL,
	@ID_UYARI			INT				= NULL
)
AS
DECLARE @return_variable BIT
EXEC dbo.spApi_OturumKontrol @TCKIMLIKNO = @TCKIMLIKNO, @OTURUM = @OTURUM, @return_variable = @return_variable output
IF @return_variable = 1
BEGIN
	DECLARE @AKTIFDONEM VARCHAR(4) = (SELECT TOP 1 DONEM FROM OkyanusDB.dbo.v3AktifDonem WHERE AKTIF = 1)
	IF @ISLEM = 1 -- ANASAYFA BİLGİ GETİR
	BEGIN
		SELECT
		(
			SELECT			
			ISNULL(
			(
				SELECT	MESAJSAYISI = COUNT(*)
				FROM OkyanusDB.dbo.v3Kullanici K  WITH (NOLOCK) 
				INNER JOIN MesajDetayV2 MD	ON MD.TC_ALAN = K.TCKIMLIKNO AND MD.OKUNDU = 0 AND MD.SIL = 0
				INNER JOIN MesajV2		M	ON M.ID_MESAJ = MD.ID_MESAJ
				WHERE  CAST(M.TARIH AS DATE) > CAST('01.09.' +@AKTIFDONEM AS DATE) AND K.TCKIMLIKNO = @TCKIMLIKNO
			), 0) AS MESAJSAYISI,
			ISNULL(
			(
				SELECT FOTOGRAF = CONVERT(VARCHAR(MAX), CAST(R.FOTOGRAF AS VARBINARY(MAX)), 2)
				FROM OkyanusDB.dbo.v3Kullanici K  WITH (NOLOCK) 
				INNER JOIN OkyanusDB.dbo.v3Resim R ON R.TCKIMLIKNO = K.TCKIMLIKNO
				WHERE  K.TCKIMLIKNO = @TCKIMLIKNO
			), '') AS FOTOGRAF
			FOR JSON PATH, INCLUDE_NULL_VALUES, WITHOUT_ARRAY_WRAPPER
		)
	END

	IF @ISLEM = 2 -- ÖĞRENMEYİ ÖĞRENİYORUM DETAY
	BEGIN
		SELECT
		ISNULL(
		(
			SELECT 
				 I.ID_ICERIK
				,I.KYE_TCKIMLIKNO
				,KYE_TARIH			= CONVERT(VARCHAR(10), I.KYE_TARIH, 104) +' '+ CONVERT(VARCHAR(5), I.KYE_TARIH, 108) +' '+ DATENAME(DW, I.KYE_TARIH)
				,I.KONU
				,I.ACIKLAMA
				,TARIH		= CONVERT(VARCHAR(10), I.TARIH, 104)
				,GUN		= DATENAME(DW, I.TARIH)
				,ADSOYAD	= K.AD +' '+ K.SOYAD
				,SINIF		= ISNULL(S1.AD, S2.AD)
				,ISNULL(S1.ID_SINIF, S2.ID_SINIF) AS ID_SINIF	
				,ISNULL(G.ID_KADEME3, S2.ID_KADEME3) AS ID_KADEME3
				,ISNULL(G.ID_SUBE, S2.ID_SUBE) AS ID_SUBE
			FROM Icerik									I
			INNER JOIN OkyanusDB.dbo.v3Kullanici		K	 WITH (NOLOCK) ON	K.TCKIMLIKNO	= I.KYE_TCKIMLIKNO
			LEFT JOIN OkyanusDB.dbo.v3Sinif			S1	ON	S1.ID_SINIF		= I.ID_SINIF
			LEFT JOIN OkyanusDB.dbo.v3SubeGrupSinif	G	ON	G.ID_SINIF		= S1.ID_SINIF
			LEFT JOIN OkyanusDB.dbo.v4Sinif			S2	ON	S2.ID_SINIF		= I.ID_SINIF AND S2.AKTIF = 1
			WHERE I.ID_ICERIK = @ID_ICERIK AND (S1.ID_SINIF IS NOT NULL OR S2.ID_SINIF IS NOT NULL)
			FOR JSON PATH
		), '[]')
	END

	IF @ISLEM = 3 -- ÖĞRENMEYİ ÖĞRENİYORUM DOSYA GETİR
	BEGIN
		SELECT
		(
			SELECT 
				ID_ICERIKDOSYA, ID_ICERIK, DOSYAORJINALAD, DOSYAAD
			FROM [dbo].[IcerikDosya]
			WHERE ID_ICERIK = @ID_ICERIK
			FOR JSON PATH
		)
	END

	IF @ISLEM = 4 -- ÖĞRENMEYİ ÖĞRENİYORUM LİNK GETİR
	BEGIN
		SELECT
		(
			SELECT 
				ID_ICERIKLINK, ID_ICERIK, LINKURL, LINKACIKLAMA
			FROM [dbo].[IcerikLink]
			WHERE ID_ICERIK = @ID_ICERIK
			FOR JSON PATH
		)
	END
	
	IF @ISLEM = 5 -- LOGİN
	BEGIN
		IF NOT EXISTS (SELECT * FROM LoginLog WHERE KYE_KULLANICI = @TCGIRISYAPAN AND KYE_OTURUM = @OTURUMGIRISYAPAN) AND @TCGIRISYAPAN <> @TCKIMLIKNO_LOGIN
		BEGIN
			RAISERROR('TC, Oturum Eşleşmiyor!', 16, 1)
		END
		ELSE
		BEGIN
			DECLARE @ID_KULLANICIGIRISYAPAN INT = (SELECT ID_KULLANICI FROM OkyanusDB.dbo.v3Kullanici  WITH (NOLOCK)  WHERE TCKIMLIKNO = @TCGIRISYAPAN)
			IF	(SELECT dbo.Fn_MenuYetkiKontrol(@ID_KULLANICIGIRISYAPAN, 148)) = 1
			OR	(SELECT dbo.Fn_MenuYetkiKontrol(@ID_KULLANICIGIRISYAPAN, 150)) = 1
			BEGIN
				SELECT  
					ID_KULLANICI AS ID,
					Ad,
					Soyad,
					(SELECT TOP 1 ID_SUBE FROM OkyanusDB.dbo.v3SubeYetki WHERE TCKIMLIKNO=k.TCKIMLIKNO ORDER BY ID_KULLANICITIPI DESC) AS ID_SUBE,
					OTURUM as oturum,
					ISNULL(TCKIMLIKNO,'') AS TCNO,
					CASE WHEN EXISTS (SELECT TOP 1 ID_SUBE FROM OkyanusDB.dbo.v3SubeYetki WHERE TCKIMLIKNO = k.TCKIMLIKNO AND ID_KULLANICITIPI = 1) THEN 1 ELSE (SELECT TOP 1 ID_KULLANICITIPI FROM OkyanusDB.dbo.v3SubeYetki WHERE TCKIMLIKNO=k.TCKIMLIKNO ORDER BY ID_KULLANICITIPI DESC) END AS KullaniciTipi,
					'' AS GUID
				INTO #temp
				FROM OkyanusDB.dbo.v3Kullanici k WITH (NOLOCK) 
				WHERE K.TCKIMLIKNO = @TCKIMLIKNO_LOGIN

				SELECT
				(
					SELECT ID,
						--(SELECT [dbo].[TurkceKarakter] (AD))	AS Ad,
						--(SELECT [dbo].[TurkceKarakter] (SOYAD)) AS Soyad ,
						AD		AS Ad,
						SOYAD	AS Soyad,
						ID_SUBE,
						oturum,
						TCNO,
						KullaniciTipi,
						GUID,
						TCGIRISYAPAN = @TCGIRISYAPAN,
						OTURUMGIRISYAPAN = @OTURUMGIRISYAPAN
					FROM #temp	
					FOR JSON PATH, WITHOUT_ARRAY_WRAPPER
				)

				INSERT INTO LoginLog (TCKIMLIKNO, KYE_KULLANICI, KYE_OTURUM)
				SELECT @TCKIMLIKNO_LOGIN, @TCGIRISYAPAN, @OTURUMGIRISYAPAN
			END
			ELSE 
			BEGIN
				RAISERROR('Menü yetkiniz olmadığı için bu işlemi gerçekleştiremezsiniz!', 16, 1)
			END
		END
	END

	IF @ISLEM = 6 -- LOGİN LİSTE GETİR
	BEGIN
		SELECT
		(	
			ISNULL(
			(
				SELECT * FROM
				(
					SELECT	 K.TCKIMLIKNO
							,ADSOYAD	= K.AD + ' ' + K.SOYAD 
							,EMAIL		= IIF(KB.EMAIL IS NOT NULL OR KB.EMAIL != '',KB.EMAIL,KB.EMAIL2)
							,TEL		= PT.TELEFON
							,AKTIF		= 1
					FROM OkyanusDB.dbo.v3Kullanici				K	WITH (NOLOCK)
					LEFT JOIN OkyanusDB.dbo.v3KullaniciBilgi	KB	WITH (NOLOCK)  ON	KB.TCKIMLIKNO	= K.TCKIMLIKNO
					LEFT JOIN OkyanusDB.dbo.v3PersonelTelefon	PT	WITH (NOLOCK)  ON	PT.TCKIMLIKNO	= K.TCKIMLIKNO
					WHERE K.TCKIMLIKNO = @TCGIRISYAPAN
					UNION ALL
					SELECT TOP 5 
						 TCKIMLIKNO	= X.TCKIMLIKNO
						,ADSOYAD	= X.ADSOYAD
						,EMAIL		= IIF ( EXISTS( SELECT TOP 1 1 FROM OkyanusDB.DBO.vw_KullaniciYetki KY WHERE KY.TCKIMLIKNO = X.TCKIMLIKNO AND KY.ID_KULLANICITIPI NOT IN(4,5)),
										IIF((KB.EMAIL IS NOT NULL OR KB.EMAIL != '') 										
											,KB.EMAIL
											,KB.EMAIL2)
										,'')						
						,TEL		= PT.TELEFON
						--,TEL		= IIF ( EXISTS( SELECT TOP 1 1 FROM OkyanusDB.DBO.vw_KullaniciYetki KY WHERE KY.TCKIMLIKNO = X.TCKIMLIKNO AND KY.ID_KULLANICITIPI NOT IN(4,5)),
						--				IIF((KB.TEL1  IS NOT NULL OR KB.TEL1  != '')										
						--					,KB.TEL1
						--					,KB.TEL2)
						--				,'')
						,AKTIF
					FROM
					(
						SELECT MAX(L.ID_LOGINLOG) AS ID_LOGINLOG, L.TCKIMLIKNO, K.AD + ' ' + K.SOYAD AS ADSOYAD, L.AKTIF
						FROM LoginLog L
						INNER JOIN OkyanusDB.dbo.v3Kullanici K  WITH (NOLOCK)  ON K.TCKIMLIKNO = L.TCKIMLIKNO
						WHERE L.KYE_KULLANICI = @TCGIRISYAPAN AND L.TCKIMLIKNO <> @TCGIRISYAPAN
						GROUP BY L.TCKIMLIKNO, K.AD, K.SOYAD, L.AKTIF
					) AS X
					LEFT JOIN OkyanusDB.dbo.v3KullaniciBilgi	KB	WITH (NOLOCK)  ON	KB.TCKIMLIKNO	= X.TCKIMLIKNO
					LEFT JOIN OkyanusDB.dbo.v3PersonelTelefon	PT	WITH (NOLOCK)  ON	PT.TCKIMLIKNO	= X.TCKIMLIKNO
					ORDER BY ID_LOGINLOG DESC
				) AS X
				WHERE AKTIF = 1
				FOR JSON PATH
			), '[]')
		)
	END

	IF @ISLEM = 7 -- VELİ ÖĞRENCİ GETİR
	BEGIN
		SELECT
		ISNULL(
		(
			SELECT DISTINCT k.AD,
				k.SOYAD,
				o.ID_OGRENCI,
				k.ID_KULLANICI as OGR_ID_KULLANICI,
				ISNULL(k.OTURUM,'') AS OGR_OTURUM,
				'' as OGR_GUID,
				o.TCKIMLIKNO
			FROM okyanusdb.dbo.v3OgrenciVeli v 
			JOIN OkyanusDB.dbo.v3Ogrenci o		ON o.TCKIMLIKNO	= v.TCKIMLIKNO_OGR
			JOIN OkyanusDB.dbo.v3Kullanici k  WITH (NOLOCK) 	ON k.TCKIMLIKNO	= o.TCKIMLIKNO
			WHERE v.TCKIMLIKNO = @TCKIMLIKNO
			AND k.AD IS NOT NULL
			AND k.AKTIF=1
			ORDER BY o.TCKIMLIKNO
			FOR JSON PATH
		), '[]')
	END

	IF @ISLEM = 8 -- SORUN BİLDİRİM	EKLE
	BEGIN
		INSERT INTO [SayfaSorunBildirim] ([SAYFAAD],[SORUN],[ID_KULLANICITIP],[ID_KULLANICI],EMAIL,ILETISIMTURU,TELEFON,TCKIMLIKNO)
		VALUES(@SAYFA, @SORUN, @ID_KULLANICITIP, @ID_KULLANICI, @EMAIL, @ILETISIMTURU, @TELEFON, @TCKIMLIKNO)
	END

	IF @ISLEM = 9 -- ÖĞRENCİ VELİ SEÇİM
	BEGIN
		DECLARE @TC_OGR VARCHAR(11)=(SELECT TCKIMLIKNO FROM OkyanusDB.dbo.v3Ogrenci WHERE ID_OGRENCI = @ID_OGRENCI)
		UPDATE OkyanusDB.dbo.v3OgrenciVeli SET AKTIF=0 WHERE TCKIMLIKNO=@TCKIMLIKNO
		UPDATE OkyanusDB.dbo.v3OgrenciVeli SET AKTIF=1 WHERE TCKIMLIKNO=@TCKIMLIKNO AND TCKIMLIKNO_OGR = @TC_OGR
	END

	IF @ISLEM = 10 -- ÖDEV GETİR
	BEGIN
		IF @ID_OGRENCI = 0
		BEGIN
			DECLARE @TC_OGRENCI VARCHAR(11)
			SET @TC_OGRENCI = @TCKIMLIKNO
		END
		ELSE
		BEGIN
			SET @TC_OGRENCI=(SELECT TCKIMLIKNO FROM OkyanusDB.dbo.v3Ogrenci WHERE ID_OGRENCI = @ID_OGRENCI)
		END

		SELECT
		ISNULL
		(
		(
			SELECT TOP 20
				O.ID_ODEV,
				ISNULL(O.ID_DERS, 0)						AS ID_DERS,
				ISNULL(D.DERSAD, '')						AS DERSAD,
				ISNULL(O.BASLIK, '')						AS BASLIK,
				CONVERT(VARCHAR ,O.TESLIM_TARIHI ,104)		AS TESLIMTARIHI
			FROM Pusulam.dbo.Odev					O	WITH(NOLOCK)
			INNER JOIN Pusulam.dbo.OdevSinif		OS	WITH(NOLOCK) ON OS.ID_ODEV = O.ID_ODEV
			INNER JOIN Pusulam.dbo.OdevSinifOgrenci	OSO WITH(NOLOCK) ON OSO.ID_ODEVSINIF = OS.ID_ODEVSINIF
			LEFT JOIN Ders							D	WITH(NOLOCK) ON	D.ID_DERS = O.ID_DERS
			WHERE O.AKTIF = 1 
			AND O.DONEM = @AKTIFDONEM
			AND	OSO.TCKIMLIKNO = @TC_OGRENCI 
			ORDER BY O.VERILIS_TARIHI DESC
			FOR JSON PATH
		), '[]')
	END

	IF @ISLEM = 11 -- LOGİN SİL
	BEGIN
		UPDATE LoginLog SET AKTIF = 0 WHERE TCKIMLIKNO = @TCKIMLIKNO_LOGIN AND KYE_KULLANICI = @TCGIRISYAPAN
	END

	IF @ISLEM = 12 -- MESAJ GETİR
	BEGIN
		SELECT
		ISNULL(
			(
				SELECT TOP 5
				M.ID_MESAJ,
				ISNULL(K.AD, '') + ' ' + ISNULL(K.SOYAD,'') AS GONDEREN,
				RTRIM(ISNULL(M.KONU, '')) AS KONU,
				TARIH = CONVERT(VARCHAR(MAX), M.TARIH, 104) + ' ' + CONVERT(VARCHAR(MAX), M.TARIH, 108),
				FOTOGRAF = CONVERT(VARCHAR(MAX), CAST(R.FOTOGRAF AS VARBINARY(MAX)), 2)
				FROM MesajV2 M
				INNER JOIN MesajDetayV2 MD ON MD.ID_MESAJ = M.ID_MESAJ
				INNER JOIN OkyanusDB.dbo.v3Kullanici K WITH (NOLOCK) ON K.TCKIMLIKNO = MD.TC_GONDEREN 
				LEFT JOIN OkyanusDB.dbo.v3Resim R on R.TCKIMLIKNO = k.TCKIMLIKNO
				WHERE MD.TC_ALAN = @TCKIMLIKNO AND MD.SIL = 0
				ORDER BY M.ID_MESAJ DESC
				FOR JSON PATH, INCLUDE_NULL_VALUES
			), '[]') AS MESAJLIST
	END

	IF @ISLEM = 13 -- OO GETİR
	BEGIN
		
		--SELECT
		--ISNULL(
		--(
		--	SELECT DISTINCT TOP 10
		--			I.ID_ICERIK
		--		,I.KONU
		--		,TARIHSIRA = I.TARIH
		--		,TARIH		= CONVERT(VARCHAR(10), I.TARIH, 104)
		--		,ADSOYAD	= K.AD +' '+ K.SOYAD
		--		,SINIF		= ISNULL(S1.AD, S2.AD)
		--		,SUBE		= B.AD
		--	FROM		Icerik							I
		--	INNER JOIN	OkyanusDB.dbo.v3Kullanici		K  WITH (NOLOCK) 	ON	K.TCKIMLIKNO	= I.KYE_TCKIMLIKNO
		--	INNER JOIN	OkyanusDB.dbo.v3Sinif			S1	ON	S1.ID_SINIF		= I.ID_SINIF
		--	LEFT JOIN	OkyanusDB.dbo.v3SubeGrupSinif	G	ON	G.ID_SINIF		= S1.ID_SINIF
		--	LEFT JOIN	OkyanusDB.dbo.v3Ogrenci			O1	ON	O1.ID_SINIF		= I.ID_SINIF
		--	LEFT JOIN	OkyanusDB.dbo.v4Sinif			S2	ON	S2.ID_SINIF		= I.ID_SINIF AND S2.AKTIF = 1
		--	LEFT JOIN	OkyanusDB.dbo.v4SinifOgrenci	O2	ON	O2.ID_SINIF		= I.ID_SINIF AND O2.AKTIF = 1
		--	INNER JOIN	OkyanusDB.dbo.v3OgrenciVeli		V	ON	V.TCKIMLIKNO_OGR= ISNULL(O1.TCKIMLIKNO, O2.TC_OGRENCI)
		--	INNER JOIN	OkyanusDB.dbo.v3Sube			B	ON	B.ID_SUBE		= ISNULL(G.ID_SUBE, S2.ID_SUBE)
		--	WHERE (I.KYE_TCKIMLIKNO = @TCKIMLIKNO OR ISNULL(O1.TCKIMLIKNO, O2.TC_OGRENCI) = @TCKIMLIKNO	OR V.TCKIMLIKNO = @TCKIMLIKNO)
		--	ORDER BY TARIHSIRA DESC
		--	FOR JSON PATH
		--), '[]') AS OGRENMEYIOGRENIYORUMLIST


		SELECT DISTINCT TOP 10
				 I.ID_ICERIK
				,I.KONU
				,TARIHSIRA = I.TARIH
				,TARIH		= CONVERT(VARCHAR(10), I.TARIH, 104)
				,ADSOYAD	= K.AD +' '+ K.SOYAD
				,SINIF		= S1.SINIF
				,SUBE		= S1.SUBEAD
			INTO #Temp13
			FROM		Icerik							I
			INNER JOIN	OkyanusDB.dbo.v3Kullanici		K  WITH (NOLOCK) 	ON	K.TCKIMLIKNO	= I.KYE_TCKIMLIKNO
			INNER JOIN	OkyanusDB.dbo.vw_v4SinifDetay	S1	ON	S1.ID_SINIF		= I.ID_SINIF
			WHERE I.KYE_TCKIMLIKNO = @TCKIMLIKNO
			UNION
			SELECT DISTINCT TOP 10
				 I.ID_ICERIK
				,I.KONU
				,TARIHSIRA = I.TARIH
				,TARIH		= CONVERT(VARCHAR(10), I.TARIH, 104)
				,ADSOYAD	= K.AD +' '+ K.SOYAD
				,SINIF		= S1.SINIF
				,SUBE		= S1.SUBEAD
			FROM		Icerik							I
			INNER JOIN	OkyanusDB.dbo.v3Kullanici		K   ON	K.TCKIMLIKNO	= I.KYE_TCKIMLIKNO
			INNER JOIN	OkyanusDB.dbo.vw_v4SinifDetay	S1	ON	S1.ID_SINIF		= I.ID_SINIF
			INNER JOIN	OkyanusDB.dbo.vw_SinifOgrenci	O1	ON	O1.ID_SINIF		= S1.ID_SINIF
			WHERE O1.TCKIMLIKNO = @TCKIMLIKNO
			UNION
			SELECT DISTINCT TOP 10
				 I.ID_ICERIK
				,I.KONU
				,TARIHSIRA = I.TARIH
				,TARIH		= CONVERT(VARCHAR(10), I.TARIH, 104)
				,ADSOYAD	= K.AD +' '+ K.SOYAD
				,SINIF		= S1.SINIF
				,SUBE		= S1.SUBEAD
			FROM		Icerik							I
			INNER JOIN	OkyanusDB.dbo.v3Kullanici		K   ON	K.TCKIMLIKNO	= I.KYE_TCKIMLIKNO
			INNER JOIN	OkyanusDB.dbo.vw_v4SinifDetay	S1	ON	S1.ID_SINIF		= I.ID_SINIF
			INNER JOIN	OkyanusDB.dbo.vw_SinifOgrenci	O1	ON	O1.ID_SINIF		= S1.ID_SINIF
			INNER JOIN	OkyanusDB.dbo.v3OgrenciVeli		V	ON	V.TCKIMLIKNO_OGR= O1.TCKIMLIKNO
			WHERE V.TCKIMLIKNO = @TCKIMLIKNO
			ORDER BY TARIHSIRA DESC

		SELECT
		ISNULL((
			SELECT DISTINCT TOP 10
				 ID_ICERIK
				,KONU
				,TARIHSIRA
				,TARIH
				,ADSOYAD
				,SINIF
				,SUBE
			FROM		#Temp13
			ORDER BY TARIHSIRA DESC
			FOR JSON PATH
		), '[]') AS OGRENMEYIOGRENIYORUMLIST


		DROP TABLE #Temp13

	END

	IF @ISLEM = 14 -- DUYURU ANASAYFA
	BEGIN

		SELECT ID_DUYURU
		INTO #DUYURULIST14
		FROM Tbl_Duyuru TD
		WHERE	TD.BAS_TARIH <= CAST(GETDATE() AS DATE)
			AND TD.BIT_TARIH >= CAST(GETDATE() AS DATE)
			AND AKTIF = 1

		
		SELECT DISTINCT TCKIMLIKNO, ID_KADEME, ID_KADEME3, ID_SUBE
		INTO #TEMP14
		FROM OkyanusDB.dbo.v3KullaniciSubeKademe3 
		WHERE TCKIMLIKNO	= @TCKIMLIKNO

		SELECT ISNULL((
			SELECT DISTINCT --TOP 5	
				 TD.ID_DUYURU
				,TD.BASLIK
				,TD.ICERIK
				,TD.BANNER
				,TARIH			= CONVERT(VARCHAR, TD.BIT_TARIH, 104)
				,YAYINTARIHI	= CONVERT(VARCHAR, TD.BAS_TARIH, 104) +' - ' + CONVERT(VARCHAR, TD.BIT_TARIH, 104)
				,FOTOGRAF		= ISNULL(TD.FOTOGRAF,'') 
				,FOTOGRAF_MOBIL	= ISNULL(TD.FOTOGRAF_MOBIL,'') 
				,DOSYALIST	= ISNULL((	SELECT DISTINCT DOSYAYOL AS DOSYA
										FROM Tbl_DuyuruDosya DD
										WHERE DD.ID_DUYURU = TD.ID_DUYURU
										FOR JSON PATH, INCLUDE_NULL_VALUES
									),'[]')
			FROM Tbl_Duyuru	TD
			JOIN (	SELECT D.ID_DUYURU
					FROM #DUYURULIST14				D
					JOIN Tbl_DuyuruYetki			DY	ON	DY.ID_DUYURU		= D.ID_DUYURU
					JOIN OkyanusDB.dbo.v3SubeYetki	SY	ON	SY.ID_KULLANICITIPI	= DY.ID_KULLANICITIPI
														AND SY.ID_SUBE			= DY.ID_SUBE
					WHERE	DY.ID_KULLANICITIPI	NOT IN (4,5)
						AND SY.TCKIMLIKNO	= @TCKIMLIKNO
				UNION
					SELECT D.ID_DUYURU
					FROM #DUYURULIST14							D
					JOIN Tbl_DuyuruYetki						DY	ON	DY.ID_DUYURU		= D.ID_DUYURU
					JOIN OkyanusDB.dbo.v3KullaniciSinif			KS	ON	KS.ID_SINIF			= DY.ID_SINIF
					JOIN OkyanusDB.DBO.v3OgrenciVeli			OV	ON	OV.TCKIMLIKNO		= KS.TCKIMLIKNO
																	OR	OV.TCKIMLIKNO_OGR	= KS.TCKIMLIKNO
					WHERE	DY.ID_KULLANICITIPI	IN (4,5)
						AND KS.TCKIMLIKNO	= @TCKIMLIKNO
				UNION
					SELECT D.ID_DUYURU
					FROM #DUYURULIST14					D
					JOIN Tbl_DuyuruYetki				DY	ON	DY.ID_DUYURU		= D.ID_DUYURU
					JOIN #TEMP14						KS	ON	KS.ID_SUBE			= DY.ID_SUBE
															AND (	KS.ID_KADEME	= DY.ID_KADEME
																OR	KS.ID_KADEME3	= DY.ID_KADEME3)
					JOIN OkyanusDB.dbo.v3OgrenciVeli	OV	ON	OV.TCKIMLIKNO		= KS.TCKIMLIKNO
															OR	OV.TCKIMLIKNO_OGR	= KS.TCKIMLIKNO
					WHERE	DY.ID_KULLANICITIPI	IN (4,5)
						AND KS.TCKIMLIKNO	= @TCKIMLIKNO
			) SD ON SD.ID_DUYURU = TD.ID_DUYURU
			FOR JSON PATH,INCLUDE_NULL_VALUES
		),'[]')

		DROP TABLE #TEMP14
		DROP TABLE #DUYURULIST14

	END

	IF @ISLEM = 15 -- ÖDEV DETAY
	BEGIN
		SELECT 
		ISNULL((
			SELECT	 O.ID_ODEV
					,O.ID_MORPADERS
					,ISNULL(D.DERSAD, '') AS DERSAD
					,O.BASLIK
					,O.ACIKLAMA
					,LINKLIST = ISNULL((SELECT OL.AD, OL.LINK FROM Pusulam.dbo.OdevLink OL WHERE OL.ID_ODEV = O.ID_ODEV FOR JSON PATH), '[]')
					,DOSYALIST = ISNULL((SELECT OD.AD , OD.GUID, OD.UZANTI FROM Pusulam.dbo.OdevDosya OD WHERE OD.ID_ODEV = O.ID_ODEV FOR JSON PATH), '[]')
					,VERILIS_TARIHI = O.VERILIS_TARIHI
					,TESLIM_TARIHI = O.TESLIM_TARIHI
			FROM Pusulam.dbo.Odev O
			LEFT JOIN eokul_v2.dbo.Ders D ON D.ID_DERS = O.ID_DERS
			WHERE O.AKTIF = 1
			AND O.ID_ODEV = @ID_ODEV
			FOR JSON PATH, WITHOUT_ARRAY_WRAPPER
		), '{}')
	END
	
	IF @ISLEM = 16 -- KULLANICI DETAY
	BEGIN
		SELECT ISNULL((
			SELECT TOP 1 AD, SOYAD, ADSOYAD= CONCAT_WS(' ',AD,SOYAD), TCKIMLIKNO
			FROM OkyanusDB.dbo.v3Kullanici K
			WHERE K.TCKIMLIKNO = @TCKIMLIKNO
			FOR JSON PATH, WITHOUT_ARRAY_WRAPPER
		), '{}')
	END
	
	IF @ISLEM = 17 -- KULLANICI UYARI
	BEGIN
		SELECT ISNULL((
			SELECT DISTINCT U.ID_UYARI ,U.METIN, ONAY = CAST(0 AS BIT)
			FROM Pusulam.DBO.Uyari					U
			JOIN Pusulam.DBO.UyariKullaniciTipi		UKT	ON	UKT.ID_UYARI		= U.ID_UYARI
			JOIN OkyanusDB.DBO.vw_KullaniciYetki	KY	ON	KY.ID_KULLANICITIPI	= UKT.ID_KULLANICITIPI
			WHERE TCKIMLIKNO = @TCKIMLIKNO
				AND NOT EXISTS (SELECT TOP 1 1 FROM Pusulam.dbo.UyariKullanici	UK WHERE UK.TCKIMLIKNO = @TCKIMLIKNO AND ONAY = 1)
			FOR JSON PATH
		), '[]')
	END
	
	IF @ISLEM = 18 -- KULLANICI UYARI ONAY
	BEGIN
		
		IF EXISTS(SELECT TOP 1 1 FROM Pusulam.dbo.UyariKullanici WHERE TCKIMLIKNO = @TCKIMLIKNO AND ID_UYARI = @ID_UYARI)
			UPDATE Pusulam.dbo.UyariKullanici SET
				ONAY = 1
			WHERE	TCKIMLIKNO	= @TCKIMLIKNO
				AND	ID_UYARI	= @ID_UYARI
		ELSE
			INSERT Pusulam.dbo.UyariKullanici (ID_UYARI, ONAY, TCKIMLIKNO)
			VALUES (@ID_UYARI, 1, @TCKIMLIKNO)

	END

END
ELSE
BEGIN
	RAISERROR('Oturum Sonlandı', 16, 1)
END





