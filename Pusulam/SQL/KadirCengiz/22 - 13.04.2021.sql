SET IDENTITY_INSERT Menu ON; 
INSERT INTO Menu (ID_MENU, AD, ACIKLAMA, KOD, URL, RESIM, GIZLI, YARDIMHTML, OZEL)
VALUES (1386, 'Öğretmen Bilgileri', 'Öğretmen Bilgileri', '068', '#Ogretmen/OgretmenBilgileri','icon-user',0,NULL, 1)
SET IDENTITY_INSERT Menu OFF; 

go

CREATE procedure [dbo].[sp_Login]
	@TCKIMLIKNO	VARCHAR(11) = NULL,
	@OTURUM		VARCHAR(50) = NULL,

	@ISLEM		INT			= NULL,
	@SIFRE		VARCHAR(50)	= NULL,
	@UYGULAMA	INT			= NULL
AS

BEGIN


BEGIN TRY    
	    DECLARE @MSG			VARCHAR(4000)
	    DECLARE @ErrorSeverity	INT
		DECLARE @ErrorState		INT

		IF @ISLEM = 1	--	PUSULAM SİTE GİRİŞ
		BEGIN
			
			SET @OTURUM = NEWID()
			DECLARE @DIS_TCKIMLIKNO VARCHAR(11) = CAST(CAST((@TCKIMLIKNO) AS BIGINT)+1 AS VARCHAR)
			IF EXISTS (	SELECT TOP 1 1
						FROM DisOgrenci	D
						WHERE	D.TCKIMLIKNO= @DIS_TCKIMLIKNO
							AND D.SIFRE		= @SIFRE
					)
			BEGIN

			
				INSERT INTO OkyanusIletisim.dbo.LoginLog(KYE_TCKIMLIKNO, OTURUM, ID_UYGULAMA, DEVICE_ID, FCM_TOKEN)VALUES(@DIS_TCKIMLIKNO, @OTURUM, 4, 'Pusulam', '')

				SELECT
					AD,
					SOYAD,
					TCKIMLIKNO,
					0 AS CINSIYET,
					GETDATE() AS DOGUMTARIHI,
					@OTURUM AS OTURUM,
					0 KADEME,
					1 AS OV,
					0 AS OVT,
					1 AS DISOGRENCI
				FROM DisOgrenci
				WHERE TCKIMLIKNO = @DIS_TCKIMLIKNO
			END
			ELSE IF EXISTS (SELECT TOP 1 1 
							FROM v3Kullanici K
							WHERE K.TCKIMLIKNO = @TCKIMLIKNO 
								AND (	EXISTS (SELECT TOP 1 1 FROM v3Ogrenci		O WHERE O.TCKIMLIKNO = K.TCKIMLIKNO)
									OR	EXISTS (SELECT TOP 1 1 FROM v3OgrenciVeli	O WHERE O.TCKIMLIKNO = K.TCKIMLIKNO)
									)
								AND (	eokul_v2.dbo.clr_Crypto_CheckPass( @SIFRE, isnull( SIFREHASH, -1))	= 1
									OR	@SIFRE = SIFRE
									) 
								AND K.AKTIF = 1  
							)
			BEGIN
			
				INSERT INTO OkyanusIletisim.dbo.LoginLog(KYE_TCKIMLIKNO, OTURUM, ID_UYGULAMA, DEVICE_ID, FCM_TOKEN)VALUES(@TCKIMLIKNO, @OTURUM, 4, 'Pusulam', '')

				UPDATE OkyanusDb.dbo.v3Kullanici SET 
					OTURUM			= @OTURUM,
					SONGIRISTARIH	= GETDATE() 
				WHERE TCKIMLIKNO = @TCKIMLIKNO

				SELECT
					K.AD, 
					K.SOYAD, 
					K.TCKIMLIKNO,
					K.CINSIYET,
					K.DOGUMTARIHI,
					K.OTURUM,
					0 KADEME,
					1 AS OV,
					1 AS OVT,
					0 AS DISOGRENCI
				FROM v3Kullanici	K
				WHERE K.TCKIMLIKNO = @TCKIMLIKNO AND K.AKTIF = 1

			END
		END
		ELSE	--	INTERAKTİF TEN GEÇİŞ 
		BEGIN

			IF	(NOT EXISTS(SELECT TCKIMLIKNO FROM [dbo].[v3Kullanici] WHERE TCKIMLIKNO = @TCKIMLIKNO AND OTURUM = @OTURUM AND AKTIF = 1) 
				AND 
				NOT EXISTS (SELECT TOP 1 1 FROM OkyanusIletisim.dbo.LoginLog WHERE KYE_TCKIMLIKNO = @TCKIMLIKNO AND OTURUM = @OTURUM AND LOGOUT = 0))
				AND 
				NOT EXISTS(SELECT TCKIMLIKNO FROM DisOgrenci WHERE TCKIMLIKNO=@TCKIMLIKNO)
			
			BEGIN
			
				SELECT 
				@ErrorSeverity	= ERROR_SEVERITY(),
				@ErrorState		= ERROR_STATE()

				SELECT @MSG = 'Hatalı Kullanıcı Adı ya da Şifre'

				RAISERROR (@MSG,		-- Message text.
						   16,			-- Severity.
						   @ErrorState	-- State.
						   );
			
			END
			ELSE
			BEGIN
				DECLARE @OV BIT=0
				IF EXISTS(SELECT TOP 1 1 FROM v3SubeYetki WHERE TCKIMLIKNO=@TCKIMLIKNO AND ID_KULLANICITIPI IN (4))
				BEGIN
					SET @OV=1
				END

				--Login olan kullanıcı sadece veli yada sadece öğrenci olma durum kontrolü
				DECLARE @OVT BIT=0

				DROP TABLE IF EXISTS #TEMPID_KULLANICITIPI
				SELECT DISTINCT ID_KULLANICITIPI INTO #TEMPID_KULLANICITIPI FROM v3SubeYetki WHERE TCKIMLIKNO=@TCKIMLIKNO


				IF NOT EXISTS (SELECT TOP 1 1 FROM #TEMPID_KULLANICITIPI WHERE ID_KULLANICITIPI NOT IN (4,5))
				BEGIN
					SET @OVT=1
				END
				DROP TABLE IF EXISTS #TEMPID_KULLANICITIPI
			
				INSERT INTO OkyanusIletisim.dbo.LoginLog(KYE_TCKIMLIKNO, OTURUM, ID_UYGULAMA, DEVICE_ID, FCM_TOKEN)VALUES(@TCKIMLIKNO, @OTURUM, 4, 'Pusulam', '')

				IF EXISTS(SELECT TCKIMLIKNO FROM DisOgrenci WHERE TCKIMLIKNO=@TCKIMLIKNO)
				BEGIN
					SELECT
						AD,
						SOYAD,
						TCKIMLIKNO,
						0 AS CINSIYET,
						GETDATE() AS DOGUMTARIHI,
						@OTURUM AS OTURUM,
						0 KADEME,
						1 AS OV,
						0 AS OVT,
						1 AS DISOGRENCI
					FROM DisOgrenci
					WHERE TCKIMLIKNO=@TCKIMLIKNO
				END
				ELSE
				BEGIN
					-- Login Bilgileri
					SELECT
						K.AD, 
						K.SOYAD, 
						K.TCKIMLIKNO,
						K.CINSIYET,
						K.DOGUMTARIHI,
						K.OTURUM,
						--kk.ID_KADEME as KADEME,
						0 KADEME,
						@OV AS OV,
						@OVT AS OVT,
						0 AS DISOGRENCI
					FROM		[dbo].v3Kullanici				K
					--INNER JOIN	OkyanusDB.dbo.v4KullaniciKademe	kk	on	kk.TCKIMLIKNO = k.TCKIMLIKNO	
					WHERE K.TCKIMLIKNO = @TCKIMLIKNO AND K.AKTIF = 1
				END
					
			END

		END
		
END TRY
		BEGIN CATCH
			
			SELECT 
				@ErrorSeverity	= ERROR_SEVERITY(),
				@ErrorState		= ERROR_STATE()
							
			SELECT @MSG = /*'Prosedür: ' + ISNULL(ERROR_PROCEDURE(), '') + ', Satır: ' + CAST(ERROR_LINE() as varchar) + ', Hata: ' +*/ ERROR_MESSAGE()
			
			RAISERROR (@MSG , -- Message text.
					   @ErrorSeverity, -- Severity.
					   @ErrorState -- State.
					   );
		END CATCH;
	
END
GO

CREATE procedure [dbo].[sp_OgretmenBilgileri]
	@ISLEM					INT			= NULL,
	@TCKIMLIKNO				VARCHAR(11)	= NULL,
	@OTURUM					VARCHAR(36)	= NULL,
	@ID_MENU				INT			= NULL,

	@TCOGRETMENLIST			VARCHAR(MAX)= NULL

AS
BEGIN

	DECLARE @PROCNAME VARCHAR(MAX) = (SELECT OBJECT_NAME(@@PROCID))
	DECLARE @LOGJSON VARCHAR(MAX)
	SET @LOGJSON = (
		SELECT	 ISLEM					= @ISLEM	
				,TCKIMLIKNO				= @TCKIMLIKNO
				,OTURUM					= @OTURUM
				,ID_MENU				= @ID_MENU

				,TCOGRETMENLIST			= @TCOGRETMENLIST
		FOR JSON PATH, WITHOUT_ARRAY_WRAPPER	   
	)

	DECLARE @_ID_LOG INT = 0

	BEGIN TRY    
		DECLARE @_DESTEK_KONTROL INT
		EXEC @_ID_LOG = dbo.sp_OturumKontrolMenuYetkiLog @OTURUM = @OTURUM, @TCKIMLIKNO = @TCKIMLIKNO, @ID_MENU = @ID_MENU, @LOGJSON = @LOGJSON, @ISLEM = @ISLEM, @PROSEDURADI = @PROCNAME

		IF @_ID_LOG > 0
		BEGIN
			
			IF @ISLEM IN (1, 2)	--	ÖĞRETMEN BİLGİLERİ	
			BEGIN

				DECLARE @AKTIFDONEM VARCHAR(4) = OkyanusDB.dbo.fn_AktifDonem()

				DROP TABLE IF EXISTS #OGRETMENLIST
				SELECT VALUE AS TCKIMLIKNO
				INTO #OGRETMENLIST
				FROM OPENJSON (@TCOGRETMENLIST)

				DROP TABLE IF EXISTS #SINIFLIST
				SELECT S.AD, DO.TC_OGRETMEN
				INTO #SINIFLIST
				FROM eokul_v2..DersOgretmen	DO
				JOIN v3Sinif				S	ON	S.ID_SINIF		= DO.ID_SINIF
				JOIN #OGRETMENLIST			OL	ON	OL.TCKIMLIKNO	= DO.TC_OGRETMEN
				WHERE DO.DONEMKOD	= @AKTIFDONEM

				DROP TABLE IF EXISTS #DERSLIST
				SELECT DISTINCT D.AD, DO.TC_OGRETMEN
				INTO #DERSLIST
				FROM eokul_v2..DersOgretmen DO
				JOIN OkyanusDB..v3Ders		D	ON	D.ID_DERS		= DO.ID_DERS
				JOIN #OGRETMENLIST			OL	ON	OL.TCKIMLIKNO	= DO.TC_OGRETMEN
				WHERE DO.DONEMKOD	= @AKTIFDONEM

				DROP TABLE IF EXISTS #SUBELIST
				SELECT DISTINCT S.AD, SY.TCKIMLIKNO
				INTO #SUBELIST
				FROM v3SubeYetki	SY
				JOIN v3Sube			S	ON	S.ID_SUBE		= SY.ID_SUBE
				JOIN #OGRETMENLIST	OL	ON	OL.TCKIMLIKNO	= SY.TCKIMLIKNO
				WHERE SY.ID_KULLANICITIPI = 3	--	ÖĞRETMEN

				DROP TABLE IF EXISTS #KADEMELIST
				SELECT DISTINCT VK.AD, KK.TCKIMLIKNO
				INTO #KADEMELIST
				FROM OkyanusDB.dbo.v4KullaniciKademe	KK 
				JOIN OkyanusDB.dbo.v3Kademe				VK	ON	VK.ID_KADEME	= KK.ID_KADEME
				JOIN #OGRETMENLIST						OL	ON	OL.TCKIMLIKNO	= KK.TCKIMLIKNO

				DROP TABLE IF EXISTS #DERSSAAT
				SELECT COUNT(DP.TCKIMLIKNO) DERSSAAT, DP.TCKIMLIKNO
				INTO #DERSSAAT
				FROM OkyanusDB.DBO.v3DersProgram	DP 
				JOIN OkyanusDB.DBO.v3Sinif			SN	ON	SN.ID_SINIF		= DP.ID_SINIF
				JOIN #OGRETMENLIST					OL	ON	OL.TCKIMLIKNO	= DP.TCKIMLIKNO
				GROUP BY DP.TCKIMLIKNO

				DROP TABLE IF EXISTS #TEMPTABLO
				SELECT DISTINCT 
					 [AD SOYAD]			= CONCAT_WS(' ', K.AD, K.SOYAD)
					,[TC KİMLİK NO]		= O.TCKIMLIKNO
					,SUBE				= '<div '+ IIF(@ISLEM = 1 , '' , 'style="color: #4ca6d5;font-family: Tahoma;font-size: 9px;font-weight: bold; margin-top:3px;"')+'>' 
											+ STUFF(( SELECT DISTINCT '<div>* ' + S.AD + '</div>' FROM #SUBELIST	S WHERE S.TCKIMLIKNO	= O.TCKIMLIKNO FOR XML PATH(''), TYPE ).value('.', 'NVARCHAR(MAX)')  ,1,0,'')
										+'</div>'
					,KADEME				= '<div '+ IIF(@ISLEM = 1 , '' , 'style="color: #4ca6d5;font-family: Tahoma;font-size: 9px;font-weight: bold; margin-top:3px;"')+'>' 
											+ STUFF(( SELECT DISTINCT '<div>* ' + S.AD + '</div>' FROM #KADEMELIST	S WHERE S.TCKIMLIKNO	= O.TCKIMLIKNO FOR XML PATH(''), TYPE ).value('.', 'NVARCHAR(MAX)')  ,1,0,'')
										+'</div>'
					,SINIF				= '<div '+ IIF(@ISLEM = 1 , '' , 'style="color: #4ca6d5;font-family: Tahoma;font-size: 9px;font-weight: bold; margin-top:3px;"')+'>' 
											+ STUFF(( SELECT DISTINCT '<div>* ' + S.AD + '</div>' FROM #SINIFLIST	S WHERE S.TC_OGRETMEN	= O.TCKIMLIKNO FOR XML PATH(''), TYPE ).value('.', 'NVARCHAR(MAX)')  ,1,0,'')
										+'</div>'
					,DERS				= '<div '+ IIF(@ISLEM = 1 , '' , 'style="color: #4ca6d5;font-family: Tahoma;font-size: 9px;font-weight: bold; margin-top:3px;"')+'>' 
											+ STUFF(( SELECT DISTINCT '<div>* ' + S.AD + '</div>' FROM #DERSLIST	S WHERE S.TC_OGRETMEN	= O.TCKIMLIKNO FOR XML PATH(''), TYPE ).value('.', 'NVARCHAR(MAX)')  ,1,0,'')
										+'</div>'
					,[TOPLAM DERS SAATİ]= (SELECT ISNULL(S.DERSSAAT,0)  FROM #DERSSAAT	S WHERE S.TCKIMLIKNO	= O.TCKIMLIKNO)
					,EMAIL				= IIF(KB.EMAIL IS NOT NULL OR KB.EMAIL != '',KB.EMAIL,KB.EMAIL2)
					,TELEFON			= PT.TELEFON
					--,SINIFLIST	= ISNULL(( SELECT DISTINCT S.AD FROM #SINIFLIST S WHERE S.TC_OGRETMEN	= O.TCKIMLIKNO FOR JSON PATH, INCLUDE_NULL_VALUES ),'[]')
					--,DERSLIST	= ISNULL(( SELECT DISTINCT S.AD FROM #DERSLIST	S WHERE S.TC_OGRETMEN	= O.TCKIMLIKNO FOR JSON PATH, INCLUDE_NULL_VALUES ),'[]')
					--,SUBELIST	= ISNULL(( SELECT DISTINCT S.AD FROM #SUBELIST	S WHERE S.TCKIMLIKNO	= O.TCKIMLIKNO FOR JSON PATH, INCLUDE_NULL_VALUES ),'[]')
					--,KADEMELIST	= ISNULL(( SELECT DISTINCT S.AD FROM #KADEMELIST S WHERE S.TCKIMLIKNO	= O.TCKIMLIKNO FOR JSON PATH, INCLUDE_NULL_VALUES ),'[]')
				INTO #TEMPTABLO
				FROM v3Ogretmen		O
				JOIN v3Kullanici	K	ON	K.TCKIMLIKNO	= O.TCKIMLIKNO					
				JOIN #OGRETMENLIST	OL	ON	OL.TCKIMLIKNO	= K.TCKIMLIKNO
				LEFT JOIN OkyanusDB.dbo.v3KullaniciBilgi	KB	WITH (NOLOCK)  ON	KB.TCKIMLIKNO	= K.TCKIMLIKNO
				LEFT JOIN OkyanusDB.dbo.v3PersonelTelefon	PT	WITH (NOLOCK)  ON	PT.TCKIMLIKNO	= K.TCKIMLIKNO

				IF @ISLEM = 1
					SELECT ISNULL((
						SELECT *
						FROM #TEMPTABLO
						FOR JSON PATH, INCLUDE_NULL_VALUES
					),'[]')
				ELSE					
					SELECT *
					FROM #TEMPTABLO

				DROP TABLE IF EXISTS #SINIFLIST
				DROP TABLE IF EXISTS #DERSLIST
				DROP TABLE IF EXISTS #SUBELIST
				DROP TABLE IF EXISTS #KADEMELIST
				DROP TABLE IF EXISTS #DERSSAAT
				DROP TABLE IF EXISTS #OGRETMENLIST
				DROP TABLE IF EXISTS #TEMPTABLO

			END

		END
	END TRY
	BEGIN CATCH
		DECLARE @_ErrorSeverity INT;
		DECLARE @_ErrorState	INT;
		DECLARE @_MSG			VARCHAR(4000)

		SELECT	@_ErrorSeverity	= ERROR_SEVERITY(),
				@_ErrorState	= ERROR_STATE(),
				@_MSG			= ERROR_MESSAGE()

		EXEC [dbo].[sp_CustomRaiseError] @MESSAGE = @_MSG, @SEVERITY = @_ErrorSeverity, @STATE = @_ErrorState, @ID_LOG = @_ID_LOG, @ID_LOGTUR = 2
	END CATCH;
END
GO

